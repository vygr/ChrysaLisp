(include "lib/asm/func.inc")
(include "sys/link/class.inc")
(include "sys/kernel/class.inc")
(include "class/array/class.inc")
(include "class/node/class.inc")
(include "sys/statics/class.inc")

(def-method 'sys_link :usb_link)
	;started by kernel for each usb link

	(def-vars
		(struct lk_node lk_node_size)
		(ptr statics params msg data mem)
		(pubyte pdata)
		(struct node_id node_id_size)
		(int idx running))

	(push-scope)

	;decrement task count
	;don't count links in load balancing
	(assign {@sys/statics/statics} {statics})
	(assign {statics->statics_sys_task_task_count - 3} {statics->statics_sys_task_task_count})

	;init node
	(call 'sys_kernel :id :nil {node_id.node_id_node1, node_id.node_id_node2})
	(assign {0 => lk_node.lk_node_task_count => lk_node.lk_node_peer_node_id.node_id_node1}
		{lk_node.lk_node_peer_node_id.node_id_node2})

	;read params msg from kernel
	(call 'sys_mail :mymail :nil {params, pdata})

	;allocate message buffers
	(call 'sys_mem :calloc {lk_buffer_size} {mem, _})

	;start C++ usb link thread...
	(call 'sys_pii :usb_start {mem})

	;start out link and send params
	(call 'sys_mail :alloc {16} {msg, data})
	(call 'sys_task :start (cat {@} (f-path 'sys_link :out)) '(_ _ :r2 :r3 :r4))
	(assign {msg, data, &lk_node, mem} '(:r0 :r1 :r5 :r6))
	(save-net-id :r0 msg_dest)
	(assign '(:r5 :r6) '((:r1 0) (:r1 +ptr_size)))
	(call 'sys_mail :send '(:r0))

	;start in link and send params
	(call 'sys_mail :alloc {16} {msg, data})
	(call 'sys_task :start (cat {@} (f-path 'sys_link :in)) '(_ _ :r2 :r3 :r4))
	(assign {msg, data, &lk_node, mem} '(:r0 :r1 :r5 :r6))
	(save-net-id :r0 msg_dest)
	(assign '(:r5 :r6) '((:r1 0) (:r1 +ptr_size)))
	(call 'sys_mail :send '(:r0))

	;wait till link dies or told to quit...
	(loop-start)
		(call 'sys_task :sleep '(1000000))
		;check if link is running or not
		(call 'array :find {statics->statics_sys_mail_links_array, &lk_node} {_, _, idx})
		(call 'sys_pii :usb_running {mem} {running})
		(vpif {running})
			(vpif {idx = -1})
				(call 'array :push_back {statics->statics_sys_mail_links_array, &lk_node})
			(endif)
		(else)
			(vpif {idx /= -1})
				(call 'array :get_iter {statics->statics_sys_mail_links_array, idx} {_, data})
				(call 'array :erase {statics->statics_sys_mail_links_array, data})
			(endif)
		(endif)
	(loop-end)

	;stop C++ thread
	(call 'sys_pii :usb_stop {mem})

	;free message buffers
	(call 'sys_mem :free {mem})

	;restore task count
	(assign {statics->statics_sys_task_task_count + 3} {statics->statics_sys_task_task_count})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'sys_link :link)
	;started by kernel for each link

	(def-vars
		(struct lk_node lk_node_size)
		(ptr statics params msg data mem)
		(pubyte pdata)
		(long handle)
		(struct node_id node_id_size))

	(push-scope)

	;decrement task count
	;don't count links in load balancing
	(assign {@sys/statics/statics} {statics})
	(assign {statics->statics_sys_task_task_count - 3} {statics->statics_sys_task_task_count})

	;init and add link node to link array
	(call 'sys_kernel :id :nil {node_id.node_id_node1, node_id.node_id_node2})
	(assign {0 => lk_node.lk_node_task_count => lk_node.lk_node_peer_node_id.node_id_node1}
		{lk_node.lk_node_peer_node_id.node_id_node2})
	(call 'array :push_back {statics->statics_sys_mail_links_array, &lk_node})

	;read params msg from kernel
	(call 'sys_mail :mymail :nil {params, pdata})

	;open shared memory file
	(call 'sys_pii :open_shared {pdata, lk_buffer_size} {handle})

	;map shared object
	(call 'sys_pii :mmap {lk_buffer_size, handle, +mmap_shared} {mem})

	;put my towel down if seams available
	(vpifnot {mem->lk_buffer_towel})
		(assign {node_id.node_id_node1} {mem->lk_buffer_towel})
	(endif)

	;start out link and send params
	(call 'sys_mail :alloc {16} {msg, data})
	(call 'sys_task :start (cat {@} (f-path 'sys_link :out)) '(_ _ :r2 :r3 :r4))
	(assign {msg, data, &lk_node, mem} '(:r0 :r1 :r5 :r6))
	(save-net-id :r0 msg_dest)
	(assign '(:r5 :r6) '((:r1 0) (:r1 +ptr_size)))
	(call 'sys_mail :send '(:r0))

	;start in link and send params
	(call 'sys_mail :alloc {16} {msg, data})
	(call 'sys_task :start (cat {@} (f-path 'sys_link :in)) '(_ _ :r2 :r3 :r4))
	(assign {msg, data, &lk_node, mem} '(:r0 :r1 :r5 :r6))
	(save-net-id :r0 msg_dest)
	(assign '(:r5 :r6) '((:r1 0) (:r1 +ptr_size)))
	(call 'sys_mail :send '(:r0))

	;wait till link dies or told to quit...
	(loop-start)
		(call 'sys_task :sleep '(1000000))
		;exit if signaled by kernel...
	(loop-end)

	;unmap object
	(call 'sys_pii :munmap {mem, lk_buffer_size, +mmap_shared})

	;close it
	(call 'sys_pii :close_shared {pdata, handle})

	;free params msg
	(call 'sys_mail :free {params})

	;restore task count
	(assign {statics->statics_sys_task_task_count + 3} {statics->statics_sys_task_task_count})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'sys_link :out)

	(def-vars
		(ptr statics lk_node msg data tx tx_msg key)
		(struct node_id node_id_size))

	(push-scope)

	(assign {@sys/statics/statics} {statics})
	(call 'node :create '(0 +node_id_size) {key})

	;read params msg
	(call 'sys_mail :mymail :nil {msg, data})
	(assign {ptr(data + 0)} {lk_node})
	(assign {ptr(data + +ptr_size)} {tx})
	(call 'sys_mail :free {msg})

	;switch to link priority and sleep to
	;let other links join us at this priority
	(call 'sys_task :set_priority '(1))
	(call 'sys_task :sleep '(500000))

	;assign tx channel
	(call 'sys_kernel :id :nil {node_id.node_id_node1, node_id.node_id_node2})
	(vpif {tx->lk_buffer_towel = node_id.node_id_node1})
		(assign {tx + lk_buffer_chan_1} {tx})
	(else)
		(assign {tx + lk_buffer_chan_2} {tx})
	(endif)
	(assign {&tx->lk_chan_msg0} {tx_msg})

	;write messages through the shared buffer
	(loop-start)
		;do we have a free out slot
		(loop-while {tx_msg->lk_msg_status = lk_chan_status_ready} 'sleep)
			;write my task count and cpu id
			(assign {node_id.node_id_node1, node_id.node_id_node2, statics->statics_sys_task_task_count}
				{tx_msg->lk_msg_peer_node_id.node_id_node1, tx_msg->lk_msg_peer_node_id.node_id_node2, tx_msg->lk_msg_task_count})

			;see if any mail for this link
			(call 'sys_mail :ready
				{lk_node->lk_node_peer_node_id.node_id_node1,
				lk_node->lk_node_peer_node_id.node_id_node2,
				key} {msg})
			(vpif {msg})
				;copy message data
				(call 'sys_mem :copy {&msg->msg_stamp_start, &tx_msg->lk_msg_stamp, msg_stamp_end - msg_stamp_start})
				(call 'sys_mem :copy {msg->msg_frag_data, &tx_msg->lk_msg_data, (msg->msg_frag_length + +long_size - 1) & -+long_size})

				;busy status
				(vp-sync)
				(assign {lk_chan_status_busy} {tx_msg->lk_msg_status})

				;free message
				(call 'sys_mail :free {msg})

				;move on to next msg chan
				(assign {&tx_msg->lk_msg_size} {tx_msg})
				(vpif {tx_msg = &tx->lk_chan_size})
					(assign {&tx->lk_chan_msg0} {tx_msg})
				(endif)
			(else)
				;just a ping
				(assign {-1} {tx_msg->lk_msg_stamp.(-msg_stamp_start).msg_frag_length})

				;busy status
				(vp-sync)
				(assign {lk_chan_status_busy} {tx_msg->lk_msg_status})

				;move on to next msg chan
				(assign {&tx_msg->lk_msg_size} {tx_msg})
				(vpif {tx_msg = &tx->lk_chan_size})
					(assign {&tx->lk_chan_msg0} {tx_msg})
				(endif)
				(break 'sleep)
			(endif)

			;let other links run
			(call 'sys_task :sleep '(0))
		(loop-end)

		;small sleep if nothing to do
		(call 'sys_task :sleep '(1000))

		;exit if signaled by kernel...
	(loop-end)

	(call 'node :destroy {key})

	(pop-scope)
	(return)

(def-func-end)

(def-method 'sys_link :in)

	(def-vars
		(ptr lk_node msg data rx rx_msg)
		(struct node_id node_id_size))

	(push-scope)

	;read params msg
	(call 'sys_mail :mymail :nil {msg, data})
	(assign {ptr(data + 0)} {lk_node})
	(assign {ptr(data + +ptr_size)} {rx})
	(call 'sys_mail :free {msg})

	;switch to link priority and sleep to
	;let other links join us at this priority
	(call 'sys_task :set_priority '(1))
	(call 'sys_task :sleep '(1000000))

	;assign rx channel
	(call 'sys_kernel :id :nil {node_id.node_id_node1, node_id.node_id_node2})
	(vpif {rx->lk_buffer_towel = node_id.node_id_node1})
		(assign {rx + lk_buffer_chan_2} {rx})
	(else)
		(assign {rx + lk_buffer_chan_1} {rx})
	(endif)
	(assign {&rx->lk_chan_msg0} {rx_msg})

	;read messages from the shared buffer
	(loop-start)
		;check for received message
		(loop-while {rx_msg->lk_msg_status = lk_chan_status_busy})
			;read neibour task count and peer id
			(assign {rx_msg->lk_msg_peer_node_id.node_id_node1, rx_msg->lk_msg_peer_node_id.node_id_node2, rx_msg->lk_msg_task_count}
				{lk_node->lk_node_peer_node_id.node_id_node1, lk_node->lk_node_peer_node_id.node_id_node2, lk_node->lk_node_task_count})

			;is this just a ping ?
			(vpif {rx_msg->lk_msg_stamp.(-msg_stamp_start).msg_frag_length /= 0xffffffff})
				;is this msg part of a parcel for this node
				(vpif {rx_msg->lk_msg_stamp.(-msg_stamp_start).msg_total_length
					&& rx_msg->lk_msg_stamp.(-msg_stamp_start).msg_dest.net_id_node_id.node_id_node1 = node_id.node_id_node1
					&& rx_msg->lk_msg_stamp.(-msg_stamp_start).msg_dest.net_id_node_id.node_id_node2 = node_id.node_id_node2})
					;copy direct from link buffer to parcel !
					(call 'sys_mail :in {rx_msg})
				(else)
					;allocate msg, copy over data
					(call 'sys_mail :alloc {rx_msg->lk_msg_stamp.(-msg_stamp_start).msg_frag_length} {msg, data})
					(call 'sys_mem :copy {&rx_msg->lk_msg_stamp, &msg->msg_stamp_start, msg_stamp_end - msg_stamp_start})
					(call 'sys_mem :copy {&rx_msg->lk_msg_data, data, rx_msg->lk_msg_stamp.(-msg_stamp_start).msg_frag_length + +long_size - 1) & -+long_size})
					;clear status
					(assign {lk_chan_status_ready} {rx_msg->lk_msg_status})

					;send onwards
					(call 'sys_mail :send {msg})
				(endif)
			(else)
				;clear status
				(assign {lk_chan_status_ready} {rx_msg->lk_msg_status})
			(endif)

			;move on to next msg chan
			(assign {&rx_msg->lk_msg_size} {rx_msg})
			(vpif {rx_msg = &rx->lk_chan_size})
				(assign {&rx->lk_chan_msg0} {rx_msg})
			(endif)

			;let other links run
			(call 'sys_task :sleep '(0))
		(loop-end)

		;small sleep as nothing to do
		(call 'sys_task :sleep '(1000))

		;exit if signaled by kernel...
	(loop-end)

	(pop-scope)
	(return)

(def-func-end)
