(include "lib/asm/func.inc")
(include "././link/class.inc")
(include "./class.inc")
(include "././statics/class.inc")

(def-method 'sys_mail :in)
	;inputs
	;:r0 = link input msg buffer (ptr)
	;trashes
	;:r0-:r14

	(def-vars
		(ptr rx_msg msg parcel_list)
		(uint len))

	(push-scope)
	(entry 'sys_mail :in {rx_msg})

	;look up parcel in mailbox
	(assign {@sys/statics/statics.statics_sys_mail_parcel_list} {parcel_list})
	(vp-def (node next src1 src2 src3 psrc1 psrc2 psrc3 rx_msg total))
	(assign {rx_msg, parcel_list} `(,rx_msg ,next))
	(load-net-id rx_msg lk_buf_src src1 src2 src3)
	(loop-list-forward next 0 node next)
		(load-net-id node msg_src psrc1 psrc2 psrc3)
	(loop-until `(,src1 = ,psrc1) `(,src2 = ,psrc2) `(,src3 = ,psrc3))
	(vpif `(,next = 0))
		;new parcel
		(assign `((,rx_msg lk_buf_total_length)) `(,total))
		(call 'sys_mail :alloc `(,total) `(,node _))
		(assign `(,total) `((,node msg_total_length)))
		(assign-net-id rx_msg lk_buf_dest node msg_dest src1 src2 src3)
		(assign-net-id rx_msg lk_buf_src node msg_src src1 src2 src3)
		(assign {parcel_list} `(,next))
		(lh-add-at-tail next node src1)
	(endif)
	(assign `(,node) {msg})

	;timestamp last change
	(call 'host_os :pii_time :nil {msg->msg_timestamp})

	;copy fragment data
	(assign {rx_msg->lk_buf_frag_length} {len})
	(assign {msg->msg_frag_offset + len} {msg->msg_frag_offset})
	(assign {msg->msg_total_length - len} {msg->msg_total_length})
	(call 'sys_mem :copy {&rx_msg->lk_buf_data,
		msg->msg_frag_data + rx_msg->lk_buf_frag_offset, len})

	;clear link status
	(assign {lk_chan_status_ready} {rx_msg->lk_buf_status})

	;got all needed ?
	(vpifnot {msg->msg_total_length})
		;yes, remove parcel and post it
		(assign {msg} `(,node))
		(ln-remove-node node next)
		(call 'sys_mail :send {msg})
	(endif)

	(pop-scope)
	(return)

(def-func-end)
