(include "lib/asm/func.inc")
(include "././link/class.inc")
(include "class/obj/class.inc")
(include "././kernel/class.inc")
(include "././statics/class.inc")

(def-method 'sys_mail :out)
	;info
	;parcels going off chip or junk mail task

	(def-vars
		(ptr msg data statics)
		(struct node_id node_id_size))

	(vp-def (id0 id1 id2 heap msg offchip data obj frag time len off amount tot))

	(push-scope)

	(call 'sys_task :set_priority '(1))
	(call 'sys_kernel :id :nil {node_id.node_id_node1, node_id.node_id_node2})
	(assign {@sys/statics/statics} {statics})

	(loop-start 'mail_loop)
		;read parcel or junk mail
		(call 'sys_mail :mymail :nil {msg, data})
		(vpifnot {(msg->msg_dest.net_id_node_id.node_id_node1 - node_id.node_id_node1)
			| (msg->msg_dest.net_id_node_id.node_id_node2 - node_id.node_id_node2)})
			;we got some junk mail, just free it for now
			(call 'sys_mail :free {msg})
			(continue 'mail_loop)
		(endif)

		;create next parcel id
		(assign {statics->statics_sys_mail_session + 1} {statics->statics_sys_mail_session})

		;for each fragment
		(call 'host_os :pii_time :nil `(,time))
		(assign {msg, &statics->statics_sys_mail_msg_heap,
				&statics->statics_sys_mail_offchip_list,
				0, data} `(,msg ,heap ,offchip ,off ,data))
		(load-fields msg '(msg_obj msg_frag_length) `(,obj ,tot))
		(vp-cpy-rr tot amount)
		(loop-start)
			;create fragment
			(call 'sys_heap :alloc `(,heap) `(_ ,frag))
			(class/obj/ref obj id0)

			;fill in fragment
			(vp-cpy-rr amount len)
			(vp-min-cr lk_data_size len)
			(assign {statics->statics_sys_mail_session, node_id.node_id_node1,
				node_id.node_id_node2} `(,id0 ,id1 ,id2))
			(save-net-id frag msg_src id0 id1 id2)
			(assign-net-id msg msg_dest frag msg_dest id0 id1 id2)
			(save-fields
				frag '(msg_obj msg_timestamp msg_frag_length msg_frag_offset msg_total_length msg_frag_data)
				`(,obj ,time ,len ,off ,tot ,data))

			;move on
			(vp-add-rr len data)
			(vp-add-rr len off)
			(vp-sub-rr len amount)

			;queue it on the outgoing packet list
			(lh-add-at-tail offchip frag id0)
		(loop-until `(,amount = 0))

		;free parcel
		(call 'sys_mail :free `(,msg))

		;let other links get at some packets
		(call 'sys_task :sleep '(0))
	(loop-end)

	(pop-scope)
	(return)

(def-func-end)
