(include 'sys/func.inc)
(include 'gui/points/class.inc)
(include 'sys/math/class.inc)

(def-method 'points 'gen_cubic)
	;inputs
	;r0 = points object (ptr)
	;r1 = stack array object (ptr)
	;r2 = p1x (16.16)
	;r3 = p1y (16.16)
	;r4 = p2x (16.16)
	;r5 = p2y (16.16)
	;r6 = p3x (16.16)
	;r7 = p3y (16.16)
	;r8 = p4x (16.16)
	;r9 = p4y (16.16)
	;r10 = tolerance (16.16)
	;outputs
	;r0 = points object (ptr)
	;trashes
	;r1-r14

	(ptr 'this 'stack)
	(pulong 'iter_end)
	(union
		(ulong 'p1 'p2 'p3 'p4 'p12 'p23 'p34 'p123 'p234 'p1234)
		(int 'p1x 'p1y 'p2x 'p2y 'p3x 'p3y 'p4x 'p4y
			'p12x 'p12y 'p23x 'p23y 'p34x 'p34y 'p123x 'p123y 'p234x 'p234y 'p1234x 'p1234y))
	(uint 'start 'length)
	(int 'eps)

	(push-scope)
	(used-syms 'p23)
	(entry 'points 'gen_cubic {this, stack, p1x, p1y, p2x, p2y, p3x, p3y, p4x, p4y, eps})

	;output first point
	(vec-set 0)
	(call 'points 'push_back2 {this, p1x, p1y})

	(d-call 'array 'get_length {stack} {_, start})
	(loop-start)
		;calculate the mid-points
		(vec-load 6 {p1x, p1y, p2x, p2y, p3x, p3y})
		(vec-dup 4)
		(vec-load 2 {p4x, p4y})
		(vec-add 6)
		(vec-asr 6 1)
		(vec-store 2 {p34x, p34y})
		(vec-tee 4 {p12x, p12y, p23x, p23y})
		(vec-dup 2)
		(vec-load 2 {p34x, p34y})
		(vec-add 4)
		(vec-asr 4 1)
		(vec-tee 4 {p123x, p123y, p234x, p234y})
		(vec-add 2)
		(vec-asr 2 1)
		(vec-store 2 {p1234x, p1234y})

		;flatness test
		(vec-load 1 {p1x} {p3x})
		(vec-add 1)
		(vec-load 1 {p2x})
		(vec-dup 1)
		(vec-dif 3)
		(vec-load 1 {p1y} {p3y})
		(vec-add 1)
		(vec-load 1 {p2y})
		(vec-dup 1)
		(vec-dif 3)
		(vec-load 1 {p2x} {p4x})
		(vec-add 1)
		(vec-load 1 {p3x})
		(vec-dup 1)
		(vec-dif 3)
		(vec-load 1 {p2y} {p4y})
		(vec-add 1)
		(vec-load 1 {p3y})
		(vec-dup 1)
		(vec-dif 3)
		(vec-abs 4)
		(vec-sum 4)

		(vec-load 1 {eps})
		(bind '(d e) (vec-pop 2))
		(vpif `(,d <= ,e))
			;output point
			(call 'points 'push_back2 {this, p1234x, p1234y})
		(else)
			;continue subdivision
			(d-call 'array 'get_length {stack} {_, length})
			(call 'array 'set_capacity {stack, length + 8})
			(call 'array 'get_end {stack} {_, iter_end})
			(call 'array 'set_length {stack, length + 8})
			(vec-load 9 {p1234, p234, p34, p4, p1, p12, p123, p1234, iter_end})
			(vec-store-long 8 (elem 0 (vec-pop 1)))
		(endif)

		(d-call 'array 'get_length {stack} {_, length})
		(breakif {length = start})
		(call 'array 'set_length {stack, length - 4})
		(call 'array 'get_end {stack} {_, iter_end})
		(vec-load 1 {iter_end})
		(vec-load-long 4 (elem 0 (vec-top 1)))
		(vec-store 4 {p1, p2, p3, p4})
		(vec-pop 1)
	(loop-end)

	;output last point
	(call 'points 'push_back2 {this, p4x, p4y})

	(exit 'points 'gen_cubic {this})
	(pop-scope)
	(return)

(def-func-end)
