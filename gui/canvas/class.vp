(import 'sys/func.inc)
(import 'gui/sdl2.inc)
(import 'gui/text/class.inc)
(import 'gui/canvas/class.inc)
(import 'gui/flow/class.inc)
(import 'class/vector/class.inc)
(import 'gui/points/class.inc)
(import 'class/symbol/class.inc)
(import 'class/stream_str/class.inc)
(import 'class/pair/class.inc)
(import 'gui/ctx/class.inc)

(gen-new 'canvas)
(gen-class 'canvas)

(def-method 'canvas 'create)
	;inputs
	;r0 = width (pixels)
	;r1 = height (pixels)
	;r2 = aa scale (uint)
	;outputs
	;r0 = 0 if error, else canvas object (ptr)
	;trashes
	;all

	;save inputs
	(entry 'canvas 'create '(r4 r5 r6))

	;create new object
	(call 'canvas 'new nil '(r0))
	(vpif '(r0 != 0))
		;init the object
		(call 'canvas 'init `(r0 (@ ,(f-path 'canvas 'vtable)) r4 r5 r6) '(r0 r1))
		(vpif '(r1 == 0))
			;error with init
			(call 'canvas 'delete '(r0) '() 'r1)
			(vp-xor-rr r0 r0)
		(endif)
	(endif)

	(exit 'canvas 'create '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'create_shared)
	;inputs
	;r0 = source canvas object (ptr)
	;outputs
	;r0 = 0 if error, else canvas object (ptr)
	;trashes
	;all

	;save inputs
	(entry 'canvas 'create_shared '(r4))

	;create new object
	(call 'canvas 'new nil '(r0))
	(vpif '(r0 != 0))
		;init the object
		(call 'canvas 'init1 `(r0 (@ ,(f-path 'canvas 'vtable)) r4) '(r0 r1))
		(vpif '(r1 == 0))
			;error with init
			(call 'canvas 'delete '(r0) '() 'r1)
			(vp-xor-rr r0 r0)
		(endif)
	(endif)

	(exit 'canvas 'create_shared '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'init)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = vtable (pptr)
	;r2 = width (pixels)
	;r3 = height (pixels)
	;r4 = aa scale (uint)
	;outputs
	;r0 = canvas object (ptr)
	;r1 = 0 if error, else ok
	;trashes
	;all

	(ptr 'this)
	(union
		'(ptr 'vtable)
		'(ulong 'ok))

	;save inputs
	(push-scope)
	(entry 'canvas 'init {this, vtable, this->canvas_width, this->canvas_height, this->canvas_aa_scale})

	;init parent
	(s-call 'canvas 'init {this, vtable} {_, ok})
	(vpif {ok})
		(assign {0} {this->canvas_texture})
		(assign {0} {this->canvas_resize_buf})
		(assign {0} {this->canvas_resize_buf_size})
		(assign {0} {this->canvas_cx})
		(assign {0} {this->canvas_cy})

		(assign {this->canvas_aa_scale * this->canvas_width} {this->canvas_width})
		(assign {this->canvas_aa_scale * this->canvas_height} {this->canvas_height})
		(assign {this->canvas_width} {this->canvas_cx1})
		(assign {this->canvas_height} {this->canvas_cy1})
		(assign {this->canvas_width * int_size} {this->canvas_stride})

		;alloc pixel array
		(call 'array 'create nil {this->canvas_array})
		(call 'array 'set_capacity {this->canvas_array, (this->canvas_stride * this->canvas_height) >> 3})

		;edge arrays, sort interval stack
		(call 'array 'create nil {this->canvas_edges})
		(call 'array 'create nil {this->canvas_edge_tracker})
		(call 'array 'create nil {this->canvas_stack})
	(endif)

	(exit 'canvas 'init {this, ok})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'canvas 'init1)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = vtable (pptr)
	;r2 = source canvas object (ptr)
	;outputs
	;r0 = canvas object (ptr)
	;r1 = 0 if error, else ok
	;trashes
	;all

	(ptr 'this 'src)
	(union
		'(ptr 'vtable)
		'(ulong 'ok))

	;save inputs
	(push-scope)
	(entry 'canvas 'init1 {this, vtable, src})

	;init parent
	(s-call 'canvas 'init {this, vtable} {_, ok})
	(vpif {ok})
		(assign {0} {this->canvas_texture})
		(assign {0} {this->canvas_resize_buf})
		(assign {0} {this->canvas_resize_buf_size})
		(assign {0} {this->canvas_cx})
		(assign {0} {this->canvas_cy})

		(assign {src->canvas_aa_scale} {this->canvas_aa_scale})
		(assign {src->canvas_width} {this->canvas_width})
		(assign {src->canvas_height} {this->canvas_height})
		(assign {src->canvas_width} {this->canvas_cx1})
		(assign {src->canvas_height} {this->canvas_cy1})
		(assign {src->canvas_stride} {this->canvas_stride})

		;ref src pixel array
		(call 'obj 'ref {src->canvas_array} {this->canvas_array})

		;edge arrays, sort interval stack
		(call 'array 'create nil {this->canvas_edges})
		(call 'array 'create nil {this->canvas_edge_tracker})
		(call 'array 'create nil {this->canvas_stack})
	(endif)

	(exit 'canvas 'init1 {this, ok})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'canvas 'deinit)
	;inputs
	;r0 = canvas object (ptr)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;all but r0

	;kernel callback
	(vp-push r0)
	(call 'sys_task 'callback '(r0 ($ callback)))
	(vp-pop r0)
	(s-jump 'canvas 'deinit '(r0))

(vp-label 'callback)
	;inputs
	;r0 = user data

	(def-struct 'local)
		(ptr 'this)
		(ptr 'old_stack)
	(def-struct-end)

	;align stack for SDL
	(vp-cpy-rr rsp r1)
	(vp-alloc local_size)
	(vp-and-cr -16 rsp)
	(entry '((rsp local_this) (rsp local_old_stack)))

	;free edge trackers, sort intervals
	(vp-cpy-ir rsp local_this r0)
	(call 'array 'deref '((r0 canvas_edges)))
	(vp-cpy-ir rsp local_this r0)
	(call 'array 'deref '((r0 canvas_edge_tracker)))
	(vp-cpy-ir rsp local_this r0)
	(call 'array 'deref '((r0 canvas_stack)))

	;free any pixel buffer
	(vp-cpy-ir rsp local_this r0)
	(call 'array 'deref '((r0 canvas_array)))

	;free any resize buffer
	(vp-cpy-ir rsp local_this r0)
	(call 'sys_mem 'free '((r0 canvas_resize_buf)))

	;free any sdl texture
	(vp-cpy-ir rsp local_this r0)
	(vp-cpy-ir r0 canvas_texture r1)
	(vpif '(r1 != 0))
		(sdl-call 'sdl 'sdl_destroy_texture '(r1))
	(endif)

	(vp-cpy-ir rsp local_old_stack rsp)
	(vp-ret)

(def-func-end)

(def-method 'canvas 'load)
	;inputs
	;r0 = c string (pubyte)
	;r1 = flags (uint)
	;outputs
	;r0 = 0 if error, else shared canvas object (ptr)
	;trashes
	;all

	(ptr 'this 'name 'sym)
	(union
		'(ptr 'hash)
		'(ptr 'bucket))
	(union
		'(ptr 'stream)
		'(pptr 'iter))
	(uint 'flags)

	(push-scope)
	(entry 'canvas 'load {name, flags})

	;shared or not
	(vpif {flags & load_flag_shared} 'funcexit)
		;create hash map ?
		(assign {ptr($canvass)} {hash})
		(vpifnot {hash})
			(call 'hash_map 'create {0, 1} {hash})
			(assign {hash} {ptr($canvass)})
		(endif)

		;create interned name, lookup in map
		(call 'symbol 'intern_cstr {name} {sym})
		(call 'hash_map 'find {hash, sym} {_, iter, bucket})
		(switch)
		(casenot {iter})
			;open file stream
			(call 'string 'create_from_file {name} {this})
			(breakifnot {this})
			(call 'stream_str 'create {this} {stream})
			(call 'canvas 'load_cpm {stream} {this})
			(call 'stream_str 'deref {stream})
			(breakifnot {this})
			(call 'pair 'create {sym, this} {iter})
			(call 'vector 'push_back {bucket, iter})
			(call 'canvas 'create_shared {this} {this})
			(break funcexit)
		(default)
			(call 'pair 'get_second {*iter} {_, this})
			(call 'canvas 'create_shared {this} {this})
		(endswitch)
		(call 'symbol 'deref {sym})
	(else)
		;open file stream
		(call 'string 'create_from_file {name} {this})
		(breakifnot {this})
		(call 'stream_str 'create {this} {stream})
		(call 'canvas 'load_cpm {stream} {this})
		(call 'stream_str 'deref {stream})
	(endif)

	(exit 'canvas 'load {this})
	(pop-scope)
	(return)

;;;;;;;;;;;;;;;;;
; interned canvas
;;;;;;;;;;;;;;;;;

	(vp-align ptr_size)
(vp-label 'canvass)
	(vp-long 0)

(def-func-end)

(def-method 'canvas 'draw)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = draw ctx (ptr)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;all but r0

	(ptr 'this 'ctx 'surface 'temp_canvas 'old_stack)

	;align stack to 16 bytes for SDl
	(vp-cpy-rr rsp r2)
	(push-scope)
	(vp-and-cr -16 rsp)
	(entry {this, ctx, old_stack})

	;if no texture then create from pixel buffer
	(vpifnot {this->canvas_texture})
		;create SDL surface from pixel buffer, convert to texture
		(call 'canvas 'create {this->canvas_width / this->canvas_aa_scale, this->canvas_height / this->canvas_aa_scale, 1} {temp_canvas})
		(vpif {this->canvas_aa_scale == 1})
			(call 'canvas 'as_argb {temp_canvas, this})
		(else)
			(call 'canvas 'resize {temp_canvas, this})
			(call 'canvas 'as_argb {temp_canvas, temp_canvas})
		(endif)
		(sdl-call 'sdl 'sdl_create_rgb_surface_from {temp_canvas->canvas_array->array_array,
			temp_canvas->canvas_width, temp_canvas->canvas_height, 32,
			temp_canvas->canvas_stride, 0xff0000, 0xff00, 0xff, 0xff000000} {surface})
		(sdl-call 'sdl 'sdl_create_texture_from_surface {ctx->ctx_sdl_ctx, surface} {this->canvas_texture})
		(sdl-call 'sdl 'sdl_free_surface {surface})
		(call 'canvas 'deref {temp_canvas})
	(endif)

	;blit texture
	(vpif {this->canvas_texture})
		(call 'ctx 'blit {ctx, this->canvas_texture, 0xffffff, 0, 0,
			this->canvas_width / this->canvas_aa_scale, this->canvas_height / this->canvas_aa_scale})
	(endif)

	(assign {this, old_stack} '(r0 rsp))
	(pop-scope-syms)
	(return)

(def-func-end)

(def-method 'canvas 'swap)
	;inputs
	;r0 = canvas object (ptr)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;all but r0

	;kernel callback
	(vp-push r0)
	(call 'sys_task 'callback '(r0 ($ callback)))
	(vp-pop r0)
	(vp-ret)

(vp-label 'callback)
	;inputs
	;r0 = user data

	(def-struct 'local)
		(ptr 'old_stack)
	(def-struct-end)

	;align stack to 16 bytes for SDl
	(vp-cpy-rr rsp r1)
	(vp-alloc local_size)
	(vp-and-cr -16 rsp)
	(entry '(r0 (rsp local_old_stack)))

	;free any sdl texture
	(vp-cpy-ir r0 canvas_texture r1)
	(vpif '(r1 != 0))
		(vp-xor-rr r2 r2)
		(vp-cpy-ri r2 r0 canvas_texture)
		(sdl-call 'sdl 'sdl_destroy_texture '(r1))
	(endif)

	(vp-cpy-ir rsp local_old_stack rsp)
	(vp-ret)

(def-func-end)

(def-method 'canvas 'pref_size)
	;inputs
	;r0 = canvas object (ptr)
	;outputs
	;r0 = canvas object (ptr)
	;r9 = preferred width (pixels)
	;r10 = preferred height (pixels)
	;trashes
	;all but r0

	(ptr 'this)

	(push-scope)
	(entry 'canvas 'pref_size {this})
	(exit 'canvas 'pref_size {this, this->canvas_width / this->canvas_aa_scale,
									this->canvas_height / this->canvas_aa_scale})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'canvas 'to_premul)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;outputs
	;r0 = canvas object (ptr)
	;r1 = color premul (argb)
	;trashes
	;r2-r3

	(entry 'canvas 'to_premul '(r0 r1))

	(vp-cpy-rr r1 r2)
	(vp-cpy-rr r1 r3)
	(vp-shr-cr 24 r1)
	(vp-and-cr 0xff00ff r2)
	(vp-and-cr 0xff00 r3)
	(vp-mul-rr r1 r2)
	(vp-mul-rr r1 r3)
	(vp-shl-cr 24 r1)
	(vp-shr-cr 8 r2)
	(vp-shr-cr 8 r3)
	(vp-and-cr 0xff00ff r2)
	(vp-and-cr 0xff00 r3)
	(vp-add-rr r2 r1)
	(vp-add-rr r3 r1)

	(exit 'canvas 'to_premul '(r0 r1))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'to_argb)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color premul (argb)
	;outputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;trashes
	;r2-r4

	(entry 'canvas 'to_argb '(r0 r1))

	(vp-cpy-rr r1 r3)
	(vp-shr-cr 24 r3)
	(vpif '(r3 != 0))
		(vpif '(r3 != 0xff))
			(vp-cpy-rr r0 r4)
			(vp-xor-rr r2 r2)
			(vp-cpy-cr (mul 255 256) r0)
			(vp-div-rrr-u r3 r2 r0)
			(vp-cpy-rr r1 r3)
			(vp-cpy-rr r1 r2)
			(vp-shr-cr 24 r1)
			(vp-and-cr 0xff00ff r3)
			(vp-and-cr 0xff00 r2)
			(vp-mul-rr r0 r3)
			(vp-mul-rr r0 r2)
			(vp-shl-cr 24 r1)
			(vp-shr-cr 8 r3)
			(vp-shr-cr 8 r2)
			(vp-and-cr 0xff00ff r3)
			(vp-and-cr 0xff00 r2)
			(vp-add-rr r3 r1)
			(vp-add-rr r2 r1)
			(vp-cpy-rr r4 r0)
		(endif)
	(endif)

	(exit 'canvas 'to_argb '(r0 r1))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'fill)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;r2-r4

	(entry 'canvas 'fill '(r0 r1))

	(call 'canvas 'to_premul '(r0 r1) '(r0 r1))
	(vp-cpy-ir r0 canvas_array r2)
	(vp-cpy-ir r2 array_array r2)
	(vp-cpy-ir-ui r0 canvas_stride r3)
	(vp-cpy-ir-ui r0 canvas_height r4)
	(vp-mul-rr r4 r3)
	(vp-add-rr r2 r3)
	(loop-start)
		(vp-cpy-ri-i r1 r2 0)
		(vp-add-cr int_size r2)
	(loop-until '(r2 == r3))

	(exit 'canvas 'fill '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'as_argb)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = source canvas object (ptr)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;r1-r7

	(entry 'canvas 'as_argb '(r0 r1))

	(vp-cpy-ir r0 canvas_array r5)
	(vp-cpy-ir r1 canvas_array r7)
	(vp-cpy-ir r5 array_array r5)
	(vp-cpy-ir r7 array_array r7)
	(vp-cpy-ir-ui r0 canvas_stride r6)
	(vp-cpy-ir-ui r0 canvas_height r1)
	(vp-mul-rr r1 r6)
	(vp-add-rr r5 r6)
	(loop-start)
		(vp-cpy-ir-ui r7 0 r1)
		(call 'canvas 'to_argb '(r0 r1) '(r0 r1))
		(vp-cpy-ri-i r1 r5 0)
		(vp-add-cr int_size r5)
		(vp-add-cr int_size r7)
	(loop-until '(r5 == r6))

	(exit 'canvas 'as_argb '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'as_premul)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = source canvas object (ptr)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;r1-r6

	(entry 'canvas 'as_premul '(r0 r1))

	(vp-cpy-ir r0 canvas_array r5)
	(vp-cpy-ir r1 canvas_array r6)
	(vp-cpy-ir r5 array_array r5)
	(vp-cpy-ir r6 array_array r6)
	(vp-cpy-ir-ui r0 canvas_stride r4)
	(vp-cpy-ir-ui r0 canvas_height r1)
	(vp-mul-rr r1 r4)
	(vp-add-rr r5 r4)
	(loop-start)
		(vp-cpy-ir-ui r6 0 r1)
		(call 'canvas 'to_premul '(r0 r1) '(r0 r1))
		(vp-cpy-ri-i r1 r5 0)
		(vp-add-cr int_size r5)
		(vp-add-cr int_size r6)
	(loop-until '(r5 == r4))

	(exit 'canvas 'as_premul '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'set_clip)
	;inputs
	;r0 = canvas object (ptr)
	;r7 = x (pixels)
	;r8 = y (pixels)
	;r9 = x1 (pixels)
	;r10 = y1 (pixels)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;r1-r2

	(entry 'canvas 'set_clip '(r0 r7 r8 r9 r10))

	;clip check
	(switch)
	(breakif '(r7 > r9))
	(breakif '(r8 > r10))
	(vp-cpy-ir-ui r0 canvas_width r1)
	(breakif '(r7 >= r1))
	(vp-cpy-ir-ui r0 canvas_height r2)
	(breakif '(r8 >= r2))
	(breakif '(r9 < 0))
	(breakif '(r10 < 0))
	(vpif '(r7 < 0))
		(vp-xor-rr r7 r7)
	(endif)
	(vpif '(r8 < 0))
		(vp-xor-rr r8 r8)
	(endif)
	(vpif '(r9 > r1))
		(vp-cpy-rr r1 r9)
	(endif)
	(vpif '(r10 > r2))
		(vp-cpy-rr r2 r10)
	(endif)
	(vp-cpy-ri-i r7 r0 canvas_cx)
	(vp-cpy-ri-i r8 r0 canvas_cy)
	(vp-cpy-ri-i r9 r0 canvas_cx1)
	(vp-cpy-ri-i r10 r0 canvas_cy1)
	(endswitch)

	(exit 'canvas 'set_clip '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'set_pix_noclip)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;r7 = x (pixels)
	;r8 = y (pixels)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;r2-r3, r7-r8

	(entry 'canvas 'set_pix_noclip '(r0 r1 r7 r8))

	(vp-cpy-ir-ui r0 canvas_stride r2)
	(vp-cpy-ir r0 canvas_array r3)
	(vp-mul-rr r2 r8)
	(vp-cpy-ir r3 array_array r3)
	(vp-shl-cr (log2 int_size) r7)
	(vp-add-rr r8 r3)
	(vp-cpy-rd-i r1 r3 r7)

	(exit 'canvas 'set_pix_noclip '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'set_span_noclip)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;r7 = x (pixels)
	;r8 = y (pixels)
	;r9 = x1 (pixels)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;r2-r3, r7-r9

	(entry 'canvas 'set_span_noclip '(r0 r1 r7 r8 r9))

	(vp-cpy-ir-ui r0 canvas_stride r2)
	(vp-cpy-ir r0 canvas_array r3)
	(vp-cpy-ir r3 array_array r3)
	(vp-mul-rr r2 r8)
	(vp-shl-cr (log2 int_size) r7)
	(vp-shl-cr (log2 int_size) r9)
	(vp-add-rr r8 r3)
	(loop-start)
		(vp-cpy-rd-i r1 r3 r7)
		(vp-add-cr int_size r7)
	(loop-until '(r7 == r9))

	(exit 'canvas 'set_span_noclip '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'set_span)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;r7 = x (pixels)
	;r8 = y (pixels)
	;r9 = x1 (pixels)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;r2-r3, r7-r9

	(entry 'canvas 'set_span '(r0 r1 r7 r8 r9))

	;clip check
	(switch)
	(breakif '(r9 <= r7))
	(vp-cpy-ir-ui r0 canvas_cy r2)
	(breakif '(r8 < r2))
	(vp-cpy-ir-ui r0 canvas_cy1 r2)
	(breakif '(r8 >= r2))
	(vp-cpy-ir-ui r0 canvas_cx r2)
	(breakif '(r9 <= r2))
	(vp-cpy-ir-ui r0 canvas_cx1 r3)
	(breakif '(r7 >= r3))
		(vpif '(r7 < r2))
			(vp-cpy-rr r2 r7)
		(endif)
		(vpif '(r9 > r3))
			(vp-cpy-rr r3 r9)
		(endif)
		(jump 'canvas 'set_span_noclip '(r0 r1 r7 r8 r9))
	(endswitch)

	(exit 'canvas 'set_span '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'blend_pix_noclip)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;r7 = x (pixels)
	;r8 = y (pixels)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;r2-r3 r4-r8

	(entry 'canvas 'blend_pix_noclip '(r0 r1 r7 r8))

	(vp-cpy-rr r1 r2)
	(vp-cpy-cr 0xff r6)
	(vp-shr-cr 24 r2)
	(vp-cpy-cr 0x00ff00ff r4)
	(vp-cpy-cr 0xff00ff00 r5)
	(vp-sub-rr r2 r6)

	(vp-cpy-ir-ui r0 canvas_stride r2)
	(vp-cpy-ir r0 canvas_array r3)
	(vp-mul-rr r2 r8)
	(vp-cpy-ir r3 array_array r3)
	(vp-shl-cr (log2 int_size) r7)
	(vp-add-rr r8 r3)
	(vp-add-rr r3 r7)

	(vp-cpy-ir-ui r7 0 r2)
	(vp-cpy-rr r2 r3)
	(vp-and-rr r4 r2)
	(vp-and-rr r5 r3)
	(vp-mul-rr r6 r2)
	(vp-mul-rr r6 r3)
	(vp-shr-cr 8 r2)
	(vp-shr-cr 8 r3)
	(vp-and-rr r4 r2)
	(vp-and-rr r5 r3)
	(vp-add-rr r3 r2)
	(vp-add-rr r1 r2)
	(vp-cpy-ri-i r2 r7 0)

	(exit 'canvas 'blend_pix_noclip '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'blend_span_noclip)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;r7 = x (pixels)
	;r8 = y (pixels)
	;r9 = x1 (pixels)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;r2-r3 r4-r9

	(entry 'canvas 'blend_span_noclip '(r0 r1 r7 r8 r9))

	(vp-cpy-rr r1 r2)
	(vp-cpy-cr 0xff r6)
	(vp-shr-cr 24 r2)
	(vp-cpy-cr 0x00ff00ff r4)
	(vp-cpy-cr 0xff00ff00 r5)
	(vp-sub-rr r2 r6)

	(vp-cpy-ir-ui r0 canvas_stride r2)
	(vp-cpy-ir r0 canvas_array r3)
	(vp-cpy-ir r3 array_array r3)
	(vp-mul-rr r2 r8)
	(vp-shl-cr (log2 int_size) r7)
	(vp-shl-cr (log2 int_size) r9)
	(vp-add-rr r8 r3)
	(vp-add-rr r3 r7)
	(vp-add-rr r3 r9)
	(loop-start)
		(vp-cpy-ir-ui r7 0 r2)

		(vp-cpy-rr r2 r3)
		(vp-and-rr r4 r2)
		(vp-and-rr r5 r3)
		(vp-mul-rr r6 r2)
		(vp-mul-rr r6 r3)
		(vp-shr-cr 8 r2)
		(vp-shr-cr 8 r3)
		(vp-and-rr r4 r2)
		(vp-and-rr r5 r3)
		(vp-add-rr r3 r2)
		(vp-add-rr r1 r2)

		(vp-cpy-ri-i r2 r7 0)
		(vp-add-cr int_size r7)
	(loop-until '(r7 == r9))

	(exit 'canvas 'blend_span_noclip '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'blend_span)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;r7 = x (pixels)
	;r8 = y (pixels)
	;r9 = x1 (pixels)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;r2-r3 r4-r9

	(entry 'canvas 'blend_span '(r0 r1 r7 r8 r9))

	;clip check
	(switch)
	(breakif '(r9 <= r7))
	(vp-cpy-ir-ui r0 canvas_cy r2)
	(breakif '(r8 < r2))
	(vp-cpy-ir-ui r0 canvas_cy1 r2)
	(breakif '(r8 >= r2))
	(vp-cpy-ir-ui r0 canvas_cx r2)
	(breakif '(r9 <= r2))
	(vp-cpy-ir-ui r0 canvas_cx1 r3)
	(breakif '(r7 >= r3))
		(vpif '(r7 < r2))
			(vp-cpy-rr r2 r7)
		(endif)
		(vpif '(r9 > r3))
			(vp-cpy-rr r3 r9)
		(endif)
		(jump 'canvas 'blend_span_noclip '(r0 r1 r7 r8 r9))
	(endswitch)

	(exit 'canvas 'blend_span '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'plot)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;r2 = span function (ptr)
	;r7 = x (pixels)
	;r8 = y (pixels)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;all but r0

	(entry 'canvas 'plot '(r0 r1 r9 r7 r8))

	;clip check
	(switch)
	(vp-cpy-ir-ui r0 canvas_cx r3)
	(vp-cpy-ir-ui r0 canvas_cy r4)
	(vp-cpy-ir-ui r0 canvas_cx1 r5)
	(vp-cpy-ir-ui r0 canvas_cy1 r6)
	(breakif '(r7 < r3))
	(breakif '(r8 < r4))
	(breakif '(r7 >= r5))
	(breakif '(r8 >= r6))
		(call 'canvas 'to_premul '(r0 r1) '(_ r1))
		(r-jump 'canvas 'set_plot '(r0 r1 r7 r8 r9))
	(endswitch)

	(exit 'canvas 'plot '(r0))
	(vp-ret)

(def-func-end)

(def-method 'canvas 'fbox)
	;inputs
	;r0 = canvas object (ptr)
	;r1 = color (argb)
	;r2 = span function (ptr)
	;r7 = x (pixels)
	;r8 = y (pixels)
	;r9 = w (pixels)
	;r10 = h (pixels)
	;outputs
	;r0 = canvas object (ptr)
	;trashes
	;all but r0

	(ptr 'this 'span)
	(uint 'color)
	(int 'x 'y 'w 'h)

	(push-scope)
	(entry 'canvas 'fbox {this, color, span, x, y, w, h})

	;clip check
	(switch)
	(breakif {w <= 0})
	(breakif {h <= 0})
	(assign {x + w, y + h} {w, h})
	(breakif {x >= this->canvas_cx1})
	(breakif {w <= this->canvas_cx})
	(breakif {y >= this->canvas_cy1})
	(breakif {h <= this->canvas_cy})
		(vpif {x < this->canvas_cx})
			(assign {this->canvas_cx} {x})
		(endif)
		(vpif {w > this->canvas_cx1})
			(assign {this->canvas_cx1} {w})
		(endif)
		(vpif {y < this->canvas_cy})
			(assign {this->canvas_cy} {y})
		(endif)
		(vpif {h > this->canvas_cy1})
			(assign {this->canvas_cy1} {h})
		(endif)
		(call 'canvas 'to_premul {this, color} {_, color})
		(loop-start)
			(r-call 'canvas 'set_span {this, color, x, y, w, span})
			(assign {y + 1} {y})
		(loop-until {y == h})
	(endswitch)

	(exit 'canvas 'fbox {this})
	(pop-scope)
	(return)

(def-func-end)
