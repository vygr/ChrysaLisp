(include 'sys/func.inc)
(include 'gui/gui/class.inc)
(include 'gui/ctx/class.inc)
(include 'gui/region/class.inc)
(include 'gui/font/class.inc)
(include 'gui/texture/class.inc)
(include 'class/symbol/class.inc)
(include 'gui/vdu/class.inc)
(include 'class/vector/class.inc)

(gen-new 'vdu)
(gen-create 'vdu)
(gen-class 'vdu)

(def-method 'vdu 'init)
	;inputs
	;r0 = vdu object (ptr)
	;r1 = vtable (pptr)
	;outputs
	;r0 = vdu object (ptr)
	;r1 = 0 if error, else ok

	;init parent
	(s-call 'vdu 'init '(r0 r1) '(r0 r1))
	(vpif '(r1 != 0))
		;init myself
		(vp-xor-rr r2 r2)
		(assign '(r2 r2 r2 r2 r2 r2 r2)
			'((r0 vdu_font) (r0 vdu_chars) (r0 vdu_cursor_x i) (r0 vdu_cursor_y i)
			(r0 vdu_width i) (r0 vdu_height i) (r0 vdu_textures)))
	(endif)
	(vp-ret)

(def-func-end)

(def-method 'vdu 'deinit)
	;inputs
	;r0 = vdu object (ptr)
	;trashes
	;all but r0

	;save object
	(vp-push r0)

	(call 'sys_mem 'free '((r0 vdu_chars)))
	(vp-cpy-ir rsp 0 r0)
	(call 'vector 'deref_if '((r0 vdu_textures)))

	;deinit parent
	(vp-pop r0)
	(s-jump 'vdu 'deinit '(r0))

(def-func-end)

(def-method 'vdu 'draw)
	;inputs
	;r0 = view object (ptr)
	;r1 = draw ctx (ptr)
	;trashes
	;all but r0

	(ptr 'this 'ctx 'txt 'font)
	(ulong 'handle)
	(int 'x 'char 'bx 'by 'bx1 'by1)
	(uint 'w 'h 'col)

	;save inputs
	(push-scope)
	(entry 'vdu 'draw {this, ctx})

	;get properties, do checks for any changes
	(call 'vdu 'get_long_prop {this, prop_ink_color} {_, col})
	(call 'vdu 'get_long_prop {this, prop_vdu_width} {_, w})
	(call 'vdu 'get_long_prop {this, prop_vdu_height} {_, h})
	(call 'vdu 'get_prop {this, prop_font} {_, font})
	(vpif {font && font != this->vdu_font})
		(assign {font} {this->vdu_font})
		(call 'vdu 'switch_font {this})
	(endif)
	(vpif {w != this->vdu_width || h != this->vdu_height})
		(assign {w, h} {this->vdu_width, this->vdu_height})
		(call 'vdu 'switch_size {this})
	(endif)

	;draw vdu chars
	(vpif {this->vdu_font && this->vdu_chars})
		(call 'region 'bounds {ctx->ctx_dirty_region} {bx, by, bx1, by1})
		(assign {(bx - ctx->ctx_x) / this->vdu_char_width} {bx})
		(assign {(bx1 - ctx->ctx_x - 1) / this->vdu_char_width} {bx1})
		(assign {(by - ctx->ctx_y) / this->vdu_char_height} {by})
		(assign {(by1 - ctx->ctx_y - 1) / this->vdu_char_height} {by1})
		(vpif {bx1 >= this->vdu_width})
			(assign {this->vdu_width - 1} {bx1})
		(endif)
		(vpif {by1 >= this->vdu_height})
			(assign {this->vdu_height - 1} {by1})
		(endif)
		(loop-while {by <= by1})
			(assign {bx} {x})
			(loop-while {x <= bx1})
				(assign {(this->vdu_chars)[by * this->vdu_width + x]} {char})
				(vpif {char > 32 && char < 127})
					(call 'vector 'get_element {this->vdu_textures, char - 33} {_, txt})
					(call 'texture 'get_metrics {txt} {_, handle, w ,h})
					(vpif {handle})
						(call 'ctx 'blit {ctx, handle, col, x * this->vdu_char_width, by * this->vdu_char_height, w, h})
					(endif)
				(endif)
				(assign {x + 1} {x})
			(loop-end)
			(assign {by + 1} {by})
		(loop-end)
		(call 'ctx 'set_color {ctx, col})
		(call 'ctx 'filled_box {ctx, this->vdu_cursor_x * this->vdu_char_width, this->vdu_cursor_y * this->vdu_char_height, this->vdu_char_width, this->vdu_char_height})
	(endif)

	(exit 'vdu 'draw {this})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'vdu 'pref_size)
	;inputs
	;r0 = vdu object (ptr)
	;outputs
	;r9 = preferred width (pixels)
	;r10 = preferred height (pixels)
	;trashes
	;all but r0

	(ptr 'this 'font)
	(uint 'w 'h)

	;save inputs
	(push-scope)
	(entry 'vdu 'pref_size {this})

	;get properties, do checks for any changes
	(call 'vdu 'get_long_prop {this, prop_vdu_width} {_, w})
	(call 'vdu 'get_long_prop {this, prop_vdu_height} {_, h})
	(call 'vdu 'ref_prop {this, prop_font} {_, font})
	(vpif {font && font != this->vdu_font})
		(assign {font} {this->vdu_font})
		(call 'vdu 'switch_font {this})
	(endif)
	(vpif {w != this->vdu_width || h != this->vdu_height})
		(assign {w, h} {this->vdu_width, this->vdu_height})
		(call 'vdu 'switch_size {this})
	(endif)
	(call 'font 'deref_if {font})

	(exit 'vdu 'pref_size {this, this->vdu_width * this->vdu_char_width,
		this->vdu_height * this->vdu_char_height})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'vdu 'switch_font)
	;inputs
	;r0 = vdu object (ptr)
	;trashes
	;all but r0

	(ptr 'this)

	;save inputs
	(push-scope)
	(entry 'vdu 'switch_font {this})

	;set char size
	(call 'font 'get_metrics {this->vdu_font} {_, _, _, this->vdu_char_height})
	(assign {this->vdu_char_height * 9 >> 4} {this->vdu_char_width})

	;ref char textures
	(call 'vector 'deref_if {this->vdu_textures})
	(call 'font 'ref_chars {this->vdu_font} {_, this->vdu_textures})

	(exit 'vdu 'switch_font {this})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'vdu 'switch_size)
	;inputs
	;r0 = vdu object (ptr)
	;trashes
	;all but r0

	(ptr 'this)
	(ulong 'size)

	;save inputs
	(push-scope)
	(entry 'vdu 'switch_size {this})

	(call 'sys_mem 'free {this->vdu_chars})
	(call 'sys_mem 'alloc {this->vdu_width * this->vdu_height} {this->vdu_chars, size})
	(call 'sys_mem 'fill {this->vdu_chars, size, char_space})

	(exit 'vdu 'switch_size {this})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'vdu 'print_char)
	;inputs
	;r0 = vdu object (ptr)
	;r1 = char (uint)
	;outputs
	;r0 = vdu object (ptr)

	(ptr 'this)
	(uint 'char)

	;save inputs
	(push-scope)
	(entry 'vdu 'print_char {this, char})

	(vpif {this->vdu_chars})
		;dirty old cursor area
		(call 'vdu 'add_dirty {this, this->vdu_cursor_x * this->vdu_char_width,
			this->vdu_cursor_y * this->vdu_char_height,
			this->vdu_char_width, this->vdu_char_height})

		;wrap cursor, maybe scroll
		(vpif {this->vdu_cursor_x >= this->vdu_width})
			(assign {0} {this->vdu_cursor_x})
			(assign {this->vdu_cursor_y + 1} {this->vdu_cursor_y})
		(endif)
		(vpif {this->vdu_cursor_y >= this->vdu_height})
			(assign {0} {this->vdu_cursor_x})
			(assign {this->vdu_height - 1} {this->vdu_cursor_y})
			(call 'sys_mem 'copy {this->vdu_chars + this->vdu_width, this->vdu_chars, (this->vdu_height - 1) * this->vdu_width})
			(call 'sys_mem 'fill {this->vdu_chars + (this->vdu_height - 1) * this->vdu_width, this->vdu_width, char_space})
			(call 'vdu 'dirty {this})
		(endif)

		(vpif {char == char_lf})
			;line feed
			(assign {0} {this->vdu_cursor_x})
			(assign {this->vdu_cursor_y + 1} {this->vdu_cursor_y})
		(elseif {char == char_tab})
			;tab
			(assign {4 - (this->vdu_cursor_x & 3)} {char})
			(loop-start)
				(call 'vdu 'print_char {this, 32})
				(assign {char - 1} {char})
			(loop-untilnot {char})
		(elseif {char == char_backspace})
			;backspace
			(vpif {this->vdu_cursor_x})
				(assign {this->vdu_cursor_x - 1} {this->vdu_cursor_x})
				(assign {32} {(this->vdu_chars)[this->vdu_cursor_y * this->vdu_width + this->vdu_cursor_x]})
				(call 'vdu 'add_dirty {this, this->vdu_cursor_x * this->vdu_char_width,
					this->vdu_cursor_y * this->vdu_char_height, this->vdu_char_width, this->vdu_char_height})
			(endif)
		(elseif {char == 129})
			;clear line
			(vpif {this->vdu_cursor_x})
				(call 'sys_mem 'fill {this->vdu_chars + this->vdu_cursor_y * this->vdu_width, this->vdu_width, char_space})
				(call 'vdu 'add_dirty {this, 0, this->vdu_cursor_y * this->vdu_char_height,
					this->vdu_cursor_x * this->vdu_char_width, this->vdu_char_height})
				(assign {0} {this->vdu_cursor_x})
			(endif)
		(elseif {char >= char_space && char < 127})
			;printable char
			(assign {char} {(this->vdu_chars)[this->vdu_cursor_y * this->vdu_width + this->vdu_cursor_x]})
			(call 'vdu 'add_dirty {this, this->vdu_cursor_x * this->vdu_char_width,
				this->vdu_cursor_y * this->vdu_char_height, this->vdu_char_width, this->vdu_char_height})
			(assign {this->vdu_cursor_x + 1} {this->vdu_cursor_x})
		(endif)

		;dirty new cursor area
		(call 'vdu 'add_dirty {this, this->vdu_cursor_x * this->vdu_char_width,
			this->vdu_cursor_y * this->vdu_char_height, this->vdu_char_width, this->vdu_char_height})
	(endif)

	(exit 'vdu 'print_char {this})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'vdu 'print_cstr)
	;inputs
	;r0 = vdu object (ptr)
	;r1 = c string pointer (pubyte)
	;outputs
	;r0 = vdu object (ptr)

	(ptr 'this)
	(pubyte 'charp)

	;save inputs
	(push-scope)
	(entry 'vdu 'print_cstr {this, charp})

	;print chars
	(loop-while {*charp})
		(call 'vdu 'print_char {this, *charp})
		(assign {charp + 1} {charp})
	(loop-end)

	(exit 'vdu 'print_cstr {this})
	(pop-scope)
	(return)

(def-func-end)
