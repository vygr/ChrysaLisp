(include 'sys/func.inc)
(include 'class/pipe/class.inc)
(include 'class/msg_in/class.inc)
(include 'class/msg_out/class.inc)
(include 'class/str/class.inc)

(def-struct 'tui_msg)
	(struct 'reply_id 'id)
(def-struct-end)

(def-struct 'stdin_msg)
	(ulong 'char)
(def-struct-end)

(def-func 'apps/terminal/tui)

	(defcvar 'buf_size 256)

	(def-struct 'local)
		(ptr 'pipe)
		(pubyte 'bufp)
		(struct 'buffer buf_size)
	(def-struct-end)

	(struct 'local 'local)
	(struct 'stdin_id 'id)
	(struct 'tui_id 'id)
	(ptr 'msg 'data 'stream 'mbox_addr)
	(long 'state)
	(uint 'index)

	;init app vars
	(push-scope)
	(assign {&local.local_buffer} {local.local_bufp})
	(call 'sys_task 'mailbox nil {tui_id.id_id})
	(call 'sys_mail 'mbox_addr {tui_id.id_mbox} {mbox_addr})
	(assign {0} {local.local_pipe})

	;start stdin polling task and send it tui mailbox id
	(call 'sys_task 'start {@apps/terminal/tui_child} {_, stdin_id.id_id, _})
	(call 'sys_mail 'alloc {tui_msg_size} {msg, data})
	(assign {stdin_id.id_id} {msg->msg_dest.id_id})
	(assign {tui_id.id_id} {data->tui_msg_reply_id.id_id})
	(call 'sys_mail 'send {msg})

	;sign on
	(call 'terminal_output_cstr {"ChrysaLisp Terminal 1.3"})
	(call 'terminal_output_char {char_lf})
	(call 'terminal_output_char {char_gt})

	;app event loop
	(loop-start)
		(vpif {local.local_pipe})
			;select on multiple mailboxes
			(call 'pipe 'select {local.local_pipe, mbox_addr} {_, index})

			;which mailbox has mail ?
			(gotoif {index == 0} 'from_mymailbox)

			;output from a pipe element
			(call 'vector 'get_element {local.local_pipe->pipe_streams, index} {_, stream})
			(call 'pipe_output {&local, stream} {state})
			(vpif {state == -1})
				;EOF
				(call 'pipe 'deref {local.local_pipe})
				(assign {0} {local.local_pipe})
				(call 'terminal_output_char {char_lf})
				(call 'terminal_output_char {char_gt})
			(endif)
		(else)
		(vp-label 'from_mymailbox)
			;dispatch event to terminal
			(call 'sys_mail 'read {mbox_addr} {msg, data})
			(call 'terminal_input {&local, data->stdin_msg_char})
			(call 'sys_mail 'free {msg})
		(endif)
		(call 'sys_task 'yield)
	(loop-end)

	;clean up
	(call 'pipe 'deref {local.local_pipe})
	(pop-scope)
	(return)

(vp-label 'pipe_output)
	;inputs
	;r0 = local
	;r1 = stream object (ptr)
	;outputs
	;r0 = -1 if EOF

	(ptr 'local 'stream)
	(int 'char 'ready)

	(push-scope)
	(entry {local, stream})

	(loop-start)
		(call 'stream 'read_char {stream} {_, char})
		(breakif {char == -1})
		(call 'terminal_output_char {char})
		(call 'stream 'read_ready {stream} {_, ready})
	(loop-untilnot {ready})

	(exit {char})
	(pop-scope)
	(return)

(vp-label 'terminal_input)
	;inputs
	;r0 = local
	;r1 = char input

	(ptr 'local 'stream)
	(ulong 'length)
	(int 'char)

	(push-scope)
	(entry {local, char})

	;echo char to terminal
;	(call 'terminal_output {local, char})

	;buffer char
	(assign {char} {*local->local_bufp})
	(assign {local->local_bufp - &local->local_buffer} {length})

	;send line ?
	(vpif {char == char_lf})
		;what state ?
		(vpifnot {local->local_pipe})
			;start new pipe
			(switch)
			(case {length})
				(call 'pipe 'create {&local->local_buffer, length} {local->local_pipe})
				(breakif {local->local_pipe})
			(default)
				(call 'terminal_output_char {char_gt})
			(endswitch)
		(else)
			;feed active pipe
			(call 'pipe 'get_input {local->local_pipe} {_, stream})
			(call 'stream 'write {stream, &local->local_buffer, length})
			(call 'stream 'write_char {stream, char_lf})
			(call 'stream 'write_flush {stream})
		(endif)
		(assign {&local->local_buffer} {local->local_bufp})
	(elseif {char == char_esc})
		;esc
		(vpif {local->local_pipe})
			;feed active pipe, then EOF
			(call 'pipe 'get_input {local->local_pipe} {_, stream})
			(call 'stream 'write {stream, &local->local_buffer, length})
			(call 'stream 'write_flush {stream})
			(assign {&local->local_buffer} {local->local_bufp})

			;stop pipe
			(call 'pipe 'deref {local->local_pipe})
			(assign {0} {local->local_pipe})
		(endif)
	(elseifnot {char < char_space || char >= 127 || length == buf_size})
		;next char
		(assign {local->local_bufp + 1} {local->local_bufp})
	(endif)
	(pop-scope)
	(return)

(vp-label 'terminal_output_char)
	;inputs
	;r0 = char output

	(int 'char)

	(push-scope)
	(entry {char})

	(vpif {char == char_tab})
		;tab
		(call 'sys_pii 'write_char {1, char_space})
		(call 'sys_pii 'write_char {1, char_space})
		(call 'sys_pii 'write_char {1, char_space})
		(call 'sys_pii 'write_char {1, char_space})
	(else)
		(call 'sys_pii 'write_char {1, char})
	(endif)

	(pop-scope)
	(return)

(vp-label 'terminal_output_cstr)
	;inputs
	;r0 = c string (pubyte)

	(pubyte 'charp)

	;save inputs
	(push-scope)
	(entry {charp})

	;print chars
	(loop-while {*charp})
		(call 'terminal_output_char {*charp})
		(assign {charp + 1} {charp})
	(loop-end)

	(pop-scope)
	(return)

(def-func-end)

(def-func 'apps/terminal/tui_child)
	;stdin polling task

	(ptr 'msg 'data)
	(struct 'tui_id 'id)
	(int 'char)

	(push-scope)

	;read tui mailbox id
	(call 'sys_mail 'mymail nil {msg, data})
	(assign {data->tui_msg_reply_id.id_id} {tui_id.id_id})
	(call 'sys_mail 'free {msg})

	;poll stdin and send char to tui
	(loop-start)
	(vp-label 'nextchar)
		(call 'sys_pii 'read_char '(0) {char})
		(vpif {char})
			(call 'sys_mail 'alloc {stdin_msg_size} {msg, data})
			(assign {tui_id.id_id} {msg->msg_dest.id_id})
			(assign {char} {data->stdin_msg_char})
			(call 'sys_mail 'send {msg})
			(goto 'nextchar)
		(endif)
		(call 'sys_task 'sleep {10000})
	(loop-end)

	(pop-scope)
	(return)

(def-func-end)
