(include "lib/asm/func.inc")
(include "./class.inc")
(include "././sym/class.inc")
(include "././list/class.inc")

(gen-create :fixeds)
(gen-vtable :fixeds)
(gen-type :fixeds)

(def-method :fixeds :mul)
	;inputs
	;:r0 = fixeds object (ptr)
	;:r1 = source1 fixeds object, can be same (ptr)
	;:r2 = source2 fixeds object, can be same (ptr)
	;outputs
	;:r0 = fixeds object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r7

	(vp-rdef (dst src1 src2 dst1 idx1 idx2 e0 e1))

	(entry :fixeds :mul `(,dst ,src1 ,src2))

	(vec-map-idx (list src1 src2) (list dst) (list e0 e1)
		(list src1 src2) (list dst1) (list idx1 idx2)
		(# (vp-mul-rr %1 %0) (vp-asr-cr +fp_shift %0) %0))

	(exit :fixeds :mul `(,dst -1))
	(vp-ret)

(def-func-end)

(def-method :fixeds :div)
	;inputs
	;:r0 = fixeds object (ptr)
	;:r1 = source1 fixeds object, can be same (ptr)
	;:r2 = source2 fixeds object, can be same (ptr)
	;outputs
	;:r0 = fixeds object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r8

	(vp-rdef (e0 e1 high src1 src2 dst1 idx1 idx2 dst))

	(entry :fixeds :div `(,dst ,e1 ,high))

	(vec-map-idx (list e1 high) (list dst) (list e0 e1)
		(list src1 src2) (list dst1) (list idx1 idx2)
		(#  (errorif `(,%%1 = 0) 'error)
			(vp-shl-cr +fp_shift %0)
			(vp-ext-rr %0 high)
			(vp-div-rrr %1 high %0)
			%0))

	(exit :fixeds :div `(,dst -1))
	(vp-ret)
(vp-label 'error)
	(exit :fixeds :div `(,dst 0))
	(vp-ret)

(def-func-end)

(def-method :fixeds :mod)
	;inputs
	;:r0 = fixeds object (ptr)
	;:r1 = source1 fixeds object, can be same (ptr)
	;:r2 = source2 fixeds object, can be same (ptr)
	;outputs
	;:r0 = fixeds object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r9

	(vp-rdef (e0 e1 high src1 src2 dst1 idx1 idx2 dst t0))

	(entry :fixeds :mod `(,dst ,e1 ,high))

	(vec-map-idx (list e1 high) (list dst) (list e0 e1)
		(list src1 src2) (list dst1) (list idx1 idx2)
		(#  (errorif `(,%%1 = 0) 'error)
			(vp-cpy-rr %0 t0)
			(vp-shl-cr +fp_shift %0)
			(vp-ext-rr %0 high)
			(vp-div-rrr %1 high %0)
			(vp-asr-cr +fp_shift %0)
			(vp-mul-rr %0 %1)
			(vp-sub-rr %1 t0)
			t0))

	(exit :fixeds :mod `(,dst -1))
	(vp-ret)
(vp-label 'error)
	(exit :fixeds :mod `(,dst 0))
	(vp-ret)

(def-func-end)

(def-method :fixeds :frac)
	;inputs
	;:r0 = fixeds object (ptr)
	;:r1 = source fixeds object, can be same (ptr)
	;outputs
	;:r0 = fixeds object (ptr)
	;trashes
	;:r1-:r6

	(vp-rdef (dst src1 dst1 idx1 idx2 e0))

	(entry :fixeds :frac `(,dst ,src1))

	(vec-map-idx (list src1) (list dst) (list e0)
		(list src1) (list dst1) (list idx1 idx2)
		(# (vp-and-cr +fp_frac_mask %0) %0))

	(exit :fixeds :frac `(,dst))
	(vp-ret)

(def-func-end)

(def-method :fixeds :floor)
	;inputs
	;:r0 = fixeds object (ptr)
	;:r1 = source fixeds object, can be same (ptr)
	;outputs
	;:r0 = fixeds object (ptr)
	;trashes
	;:r1-:r5

	(vp-rdef (dst src1 dst1 idx1 idx2 e0))

	(entry :fixeds :floor `(,dst ,src1))

	(vec-map-idx (list src1) (list dst) (list e0)
		(list src1) (list dst1) (list idx1 idx2)
		(# (vp-and-cr +fp_int_mask %0) %0))

	(exit :fixeds :floor `(,dst))
	(vp-ret)

(def-func-end)

(def-method :fixeds :ceil)
	;inputs
	;:r0 = fixeds object (ptr)
	;:r1 = source fixeds object, can be same (ptr)
	;outputs
	;:r0 = fixeds object (ptr)
	;trashes
	;:r1-:r6

	(vp-rdef (dst src1 dst1 idx1 idx2 e0 t0))

	(entry :fixeds :ceil `(,dst ,src1))

	(vec-map-idx (list src1) (list dst) (list e0)
		(list src1) (list dst1) (list idx1 idx2)
		(#	(vp-cpy-rr %0 t0)
			(vp-and-cr +fp_frac_mask t0)
			(vp-and-cr +fp_int_mask %0)
			(vpif `(,t0 /= 0))
				(vp-add-cr (<< 1 +fp_shift) %0)
			(endif)
			%0))

	(exit :fixeds :ceil `(,dst))
	(vp-ret)

(def-func-end)

(def-method :fixeds :scale)
	;inputs
	;:r0 = fixeds object (ptr)
	;:r1 = source fixeds object, can be same (ptr)
	;:r2 = scale (fixed)
	;outputs
	;:r0 = fixeds object (ptr)
	;trashes
	;:r1-:r6

	(vp-rdef (dst src1 scale dst1 idx1 idx2 e0))

	(entry :fixeds :scale `(,dst ,src1 ,scale))

	(vec-map-idx (list src1) (list dst) (list e0)
		(list src1) (list dst1) (list idx1 idx2)
		(# (vp-mul-rr scale %0) (vp-asr-cr +fp_shift %0) %0))

	(exit :fixeds :scale `(,dst))
	(vp-ret)

(def-func-end)

(def-method :fixeds :dot)
	;inputs
	;:r0 = fixeds object (ptr)
	;:r1 = fixeds object, can be same (ptr)
	;outputs
	;:r0 = fixeds object (ptr)
	;:r1 = dot product (fixed)
	;trashes
	;:r1-:r7

	(vp-rdef (src dot src1 src2 idx1 idx2 e0 e1))

	(entry :fixeds :dot `(,src ,dot))

	(vec-reduce-idx (list src dot) (list e0 e1) (list src1 src2) (list idx1 idx2)
		(# (vp-mul-rr %0 %1) (vp-asr-cr +fp_shift %1) (vp-add-rr %1 dot))
		(# (vp-sub-rr dot dot)))

	(exit :fixeds :dot `(,src ,dot))
	(vp-ret)

(def-func-end)

(def-method :fixeds :mat4x4_mul)
	;inputs
	;:r0 = output fixeds object (ptr)
	;:r1 = ma fixeds object (ptr)
	;:r2 = mb fixeds object (ptr)
	;outputs
	;:r0 = output fixeds object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r13

	(vp-rdef (out ma mb data_o data_a data_b)
		'(:r0 :r1 :r2 :r3 :r4 :r5))
	(vp-rdef (a0 a1 a2 a3 b0 b1 b2 b3)
		'(:r6 :r7 :r8 :r9 :r10 :r11 :r12 :r13))

	(entry :fixeds :mat4x4_mul `(,out ,ma ,mb))

	(assign `((,ma +array_begin) (,mb +array_begin) (,out +array_begin))
		`(,data_a ,data_b ,data_o))

	(defq r 0)
	(times 4
		; Load Row A (Stride 32 bytes)
		(vp-simd vp-cpy-ir `(,data_a)
			`(,(* r 32) ,(+ (* r 32) 8) ,(+ (* r 32) 16) ,(+ (* r 32) 24))
			`(,a0 ,a1 ,a2 ,a3))

		(defq c 0)
		(times 4
			; Load column of B (strided for column-major storage)
			(vp-simd vp-cpy-ir `(,data_b)
				`(,(* c 8) ,(+ (* c 8) 32) ,(+ (* c 8) 64) ,(+ (* c 8) 96))
				`(,b0 ,b1 ,b2 ,b3))

			(vp-simd vp-mul-rr `(,a0 ,a1 ,a2 ,a3) `(,b0 ,b1 ,b2 ,b3))
			(vp-simd vp-asr-cr `(,+fp_shift) `(,b0 ,b1 ,b2 ,b3))
			(vp-simd vp-add-rr `(,b1 ,b3) `(,b0 ,b2))
			(vp-add-rr b2 b0)

			(vp-cpy-ri b0 data_o (+ (* r 32) (* c 8)))
			(setq c (inc c)))
		(setq r (inc r)))

	(exit :fixeds :mat4x4_mul `(,out -1))
	(vp-ret)

(def-func-end)

(def-method :fixeds :mat4x4_v4_mul)
	;inputs
	;:r0 = output fixeds object (ptr)
	;:r1 = matrix ma fixeds object (ptr)
	;:r2 = vector v fixeds object (ptr)
	;outputs
	;:r0 = output fixeds object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r13

	(vp-rdef (out ma v data_o data_a data_v)
		'(:r0 :r1 :r2 :r3 :r4 :r5))
	(vp-rdef (a0 a1 a2 a3 v0 v1 v2 v3)
		'(:r6 :r7 :r8 :r9 :r10 :r11 :r12 :r13))

	(entry :fixeds :mat4x4_v4_mul `(,out ,ma ,v))

	(assign `((,ma +array_begin) (,v +array_begin) (,out +array_begin))
		`(,data_a ,data_v ,data_o))

	; Load Vector V
	(vp-simd vp-cpy-ir `(,data_v) `(0 8 16 24) `(,v0 ,v1 ,v2 ,v3))

	(defq r 0)
	(times 4
		; Load Row A
		(vp-simd vp-cpy-ir `(,data_a)
			`(,(* r 32) ,(+ (* r 32) 8) ,(+ (* r 32) 16) ,(+ (* r 32) 24))
			`(,a0 ,a1 ,a2 ,a3))

		(vp-simd vp-mul-rr `(,v0 ,v1 ,v2 ,v3) `(,a0 ,a1 ,a2 ,a3))
		(vp-simd vp-asr-cr `(,+fp_shift) `(,a0 ,a1 ,a2 ,a3))
		(vp-simd vp-add-rr `(,a1 ,a3) `(,a0 ,a2))
		(vp-add-rr a2 a0)

		; Write Component
		(vp-cpy-ri a0 data_o (* r 8))
		(setq r (inc r)))

	(exit :fixeds :mat4x4_v4_mul `(,out -1))
	(vp-ret)

(def-func-end)

(def-method :fixeds :mat3x3_mul)
	;inputs
	;:r0 = output fixeds object (ptr)
	;:r1 = ma fixeds object (ptr)
	;:r2 = mb fixeds object (ptr)
	;outputs
	;:r0 = output fixeds object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r13

	(vp-rdef (out ma mb data_o data_a data_b)
		'(:r0 :r1 :r2 :r3 :r4 :r5))
	(vp-rdef (a0 a1 a2 b0 b1 b2)
		'(:r6 :r7 :r8 :r9 :r10 :r11))

	(entry :fixeds :mat3x3_mul `(,out ,ma ,mb))

	(assign `((,ma +array_begin) (,mb +array_begin) (,out +array_begin))
		`(,data_a ,data_b ,data_o))

	(defq r 0)
	(times 3
		; Load Row A (Stride 24 bytes)
		(vp-simd vp-cpy-ir `(,data_a)
			`(,(* r 24) ,(+ (* r 24) 8) ,(+ (* r 24) 16))
			`(,a0 ,a1 ,a2))

		(defq c 0)
		(times 3
			; Load column of B (strided for column-major storage)
			(vp-simd vp-cpy-ir `(,data_b)
				`(,(* c 8) ,(+ (* c 8) 24) ,(+ (* c 8) 48))
				`(,b0 ,b1 ,b2))

			(vp-simd vp-mul-rr `(,a0 ,a1 ,a2) `(,b0 ,b1 ,b2))
			(vp-simd vp-asr-cr `(,+fp_shift) `(,b0 ,b1 ,b2))
			(vp-add-rr b1 b0)
			(vp-add-rr b2 b0)

			(vp-cpy-ri b0 data_o (+ (* r 24) (* c 8)))
			(setq c (inc c)))
		(setq r (inc r)))

	(exit :fixeds :mat3x3_mul `(,out -1))
	(vp-ret)

(def-func-end)

(def-method :fixeds :mat3x3_v3_mul)
	;inputs
	;:r0 = output fixeds object (ptr)
	;:r1 = matrix ma fixeds object (ptr)
	;:r2 = vector v fixeds object (ptr)
	;outputs
	;:r0 = output fixeds object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r13

	(vp-rdef (out ma v data_o data_a data_v)
		'(:r0 :r1 :r2 :r3 :r4 :r5))
	(vp-rdef (a0 a1 a2 v0 v1 v2)
		'(:r6 :r7 :r8 :r9 :r10 :r11))

	(entry :fixeds :mat3x3_v3_mul `(,out ,ma ,v))

	(assign `((,ma +array_begin) (,v +array_begin) (,out +array_begin))
		`(,data_a ,data_v ,data_o))

	; Load Vector V
	(vp-simd vp-cpy-ir `(,data_v) `(0 8 16) `(,v0 ,v1 ,v2))

	(defq r 0)
	(times 3
		; Load Row A
		(vp-simd vp-cpy-ir `(,data_a)
			`(,(* r 24) ,(+ (* r 24) 8) ,(+ (* r 24) 16))
			`(,a0 ,a1 ,a2))

		(vp-simd vp-mul-rr `(,v0 ,v1 ,v2) `(,a0 ,a1 ,a2))
		(vp-simd vp-asr-cr `(,+fp_shift) `(,a0 ,a1 ,a2))
		(vp-add-rr a1 a0)
		(vp-add-rr a2 a0)

		; Write Component
		(vp-cpy-ri a0 data_o (* r 8))
		(setq r (inc r)))

	(exit :fixeds :mat3x3_v3_mul `(,out -1))
	(vp-ret)

(def-func-end)