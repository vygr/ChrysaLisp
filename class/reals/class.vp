(include "lib/asm/func.inc")
(include "./class.inc")
(include "././sym/class.inc")
(include "././list/class.inc")

(gen-create :reals)
(gen-vtable :reals)
(gen-type :reals)

(def-method :reals :add)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source1 reals object, can be same (ptr)
	;:r2 = source2 reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r5, :f0-:f1

	(vp-rdef (dst src1 src2 dst1 idx1 idx2))
	(vp-fdef (e0 e1))

	(entry :reals :add `(,dst ,src1 ,src2))

	(vec-map-idx (list src1 src2) (list dst) (list e0 e1)
		(list src1 src2) (list dst1) (list idx1 idx2)
		(# (vp-add-ff %1 %0) %0))

	(exit :reals :add `(,dst -1))
	(vp-ret)

(def-func-end)

(def-method :reals :sub)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source1 reals object, can be same (ptr)
	;:r2 = source2 reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r5, :f0-:f1

	(vp-rdef (dst src1 src2 dst1 idx1 idx2))
	(vp-fdef (e0 e1))

	(entry :reals :sub `(,dst ,src1 ,src2))

	(vec-map-idx (list src1 src2) (list dst) (list e0 e1)
		(list src1 src2) (list dst1) (list idx1 idx2)
		(# (vp-sub-ff %1 %0) %0))

	(exit :reals :sub `(,dst -1))
	(vp-ret)

(def-func-end)

(def-method :reals :min)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source1 reals object, can be same (ptr)
	;:r2 = source2 reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r5, :f0-:f1

	(vp-rdef (dst src1 src2 dst1 idx1 idx2))
	(vp-fdef (e0 e1))

	(entry :reals :min `(,dst ,src1 ,src2))

	(vec-map-idx (list src1 src2) (list dst) (list e0 e1)
		(list src1 src2) (list dst1) (list idx1 idx2)
		(# (vp-min-ff %1 %0) %0))

	(exit :reals :min `(,dst -1))
	(vp-ret)

(def-func-end)

(def-method :reals :max)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source1 reals object, can be same (ptr)
	;:r2 = source2 reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r5, :f0-:f1

	(vp-rdef (dst src1 src2 dst1 idx1 idx2))
	(vp-fdef (e0 e1))

	(entry :reals :max `(,dst ,src1 ,src2))

	(vec-map-idx (list src1 src2) (list dst) (list e0 e1)
		(list src1 src2) (list dst1) (list idx1 idx2)
		(# (vp-max-ff %1 %0) %0))

	(exit :reals :max `(,dst -1))
	(vp-ret)

(def-func-end)

(def-method :reals :mul)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source1 reals object, can be same (ptr)
	;:r2 = source2 reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r5, :f0-:f1

	(vp-rdef (dst src1 src2 dst1 idx1 idx2))
	(vp-fdef (e0 e1))

	(entry :reals :mul `(,dst ,src1 ,src2))

	(vec-map-idx (list src1 src2) (list dst) (list e0 e1)
		(list src1 src2) (list dst1) (list idx1 idx2)
		(# (vp-mul-ff %1 %0) %0))

	(exit :reals :mul `(,dst -1))
	(vp-ret)

(def-func-end)

(def-method :reals :div)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source1 reals object, can be same (ptr)
	;:r2 = source2 reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r5, :f0-:f2

	(vp-rdef (dst src1 src2 dst1 idx1 idx2))
	(vp-fdef (e0 e1 zero))

	(entry :reals :div `(,dst ,src1 ,src2))

	(vec-map-idx (list src1 src2) (list dst) (list e0 e1)
		(list src1 src2) (list dst1) (list idx1 idx2)
		(#  (errorif `(,%%1 = ,zero) 'error)
			(vp-div-ff %1 %0) %0)
		(# 	(vp-sub-ff zero zero)))

	(exit :reals :div `(,dst -1))
	(vp-ret)
(vp-label 'error)
	(exit :reals :div `(,dst 0))
	(vp-ret)

(def-func-end)

(def-method :reals :mod)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source1 reals object, can be same (ptr)
	;:r2 = source2 reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r6, :f0-:f3

	(vp-rdef (dst src1 src2 dst1 idx1 idx2 trunc))
	(vp-fdef (e0 e1 ftrunc zero))

	(entry :reals :mod `(,dst ,src1 ,src2))

	(vec-map-idx (list src1 src2) (list dst) (list e0 e1)
		(list src1 src2) (list dst1) (list idx1 idx2)
		(#  (errorif `(,%%1 = ,zero) 'error)
			(vp-cpy-ff %0 ftrunc)
			(vp-div-ff %1 ftrunc)
			(vp-cvt-fr ftrunc trunc)
			(vp-cvt-rf trunc ftrunc)
			(vp-mul-ff %1 ftrunc)
			(vp-sub-ff ftrunc %0)
			%0)
		(# 	(vp-sub-ff zero zero)))

	(exit :reals :mod `(,dst -1))
	(vp-ret)
(vp-label 'error)
	(exit :reals :mod `(,dst 0))
	(vp-ret)

(def-func-end)

(def-method :reals :frac)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;trashes
	;:r1-:r5, :f0-:f3

	(vp-rdef (dst src1 dst1 idx1 idx2 trunc))
	(vp-fdef (e0 ftrunc one zero))

	(entry :reals :frac `(,dst ,src1))

	(vec-map-idx (list src1) (list dst) (list e0)
		(list src1) (list dst1) (list idx1 idx2)
		(#  (vp-cvt-fr %0 trunc)
			(vp-cvt-rf trunc ftrunc)
			(vp-sub-ff ftrunc %0)
			(vpif `(,%%0 < ,zero))
				(vp-add-ff one %0)
			(endif)
			%0)
		(# (vp-sub-ff zero zero)
			(vp-cpy-cr 1 trunc)
			(vp-cvt-rf trunc one)))

	(exit :reals :frac `(,dst))
	(vp-ret)

(def-func-end)

(def-method :reals :floor)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;trashes
	;:r1-:r5, :f0-:f2

	(vp-rdef (dst src1 dst1 idx1 idx2 trunc))
	(vp-fdef (e0 ftrunc one))

	(entry :reals :floor `(,dst ,src1))

	(vec-map-idx (list src1) (list dst) (list e0)
		(list src1) (list dst1) (list idx1 idx2)
		(#  (vp-cvt-fr %0 trunc)
			(vp-cvt-rf trunc ftrunc)
			(vpif `(,ftrunc > ,%%0))
				(vp-sub-ff one ftrunc)
			(endif)
			ftrunc)
		(# 	(vp-cpy-cr 1 trunc)
			(vp-cvt-rf trunc one)))

	(exit :reals :floor `(,dst))
	(vp-ret)

(def-func-end)

(def-method :reals :ceil)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;trashes
	;:r1-:r5, :f0-:f2

	(vp-rdef (dst src1 dst1 idx1 idx2 trunc))
	(vp-fdef (e0 ftrunc one))

	(entry :reals :ceil `(,dst ,src1))

	(vec-map-idx (list src1) (list dst) (list e0)
		(list src1) (list dst1) (list idx1 idx2)
		(#  (vp-cvt-fr %0 trunc)
			(vp-cvt-rf trunc ftrunc)
			(vpif `(,ftrunc < ,%%0))
				(vp-add-ff one ftrunc)
			(endif)
			ftrunc)
		(# 	(vp-cpy-cr 1 trunc)
			(vp-cvt-rf trunc one)))

	(exit :reals :ceil `(,dst))
	(vp-ret)

(def-func-end)

(def-method :reals :abs)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;trashes
	;:r1-:r4, :f0

	(vp-rdef (dst src1 dst1 idx1 idx2))
	(vp-fdef (e0))

	(entry :reals :abs `(,dst ,src1))

	(vec-map-idx (list src1) (list dst) (list e0)
		(list src1) (list dst1) (list idx1 idx2)
		(# (vp-abs-ff %0 %0) %0))

	(exit :reals :abs `(,dst))
	(vp-ret)

(def-func-end)

(def-method :reals :scale)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = source reals object, can be same (ptr)
	;:r2 = scale (real)
	;outputs
	;:r0 = reals object (ptr)
	;trashes
	;:r1-:r4, :f0-:f1

	(vp-rdef (dst src1 dst1 idx1 idx2))
	(vp-fdef (e0 scale))

	(entry :reals :scale `(,dst ,src1 ,scale))

	(vec-map-idx (list src1) (list dst) (list e0)
		(list src1) (list dst1) (list idx1 idx2)
		(# (vp-mul-ff scale %0) %0))

	(exit :reals :scale `(,dst))
	(vp-ret)

(def-func-end)

(def-method :reals :sum)
	;inputs
	;:r0 = reals object (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;:r1 = sum (real)
	;trashes
	;:r1-:r2, :f0-:f1

	(vp-rdef (this src1 src_end))
	(vp-fdef (sum e0))

	(entry :reals :sum `(,this))

	(vec-reduce (list this) (list e0) (list src1) src_end
		(# (vp-add-ff %0 sum))
		(# (vp-sub-ff sum sum)))

	(exit :reals :sum `(,this ,sum))
	(vp-ret)

(def-func-end)

(def-method :reals :dot)
	;inputs
	;:r0 = reals object (ptr)
	;:r1 = reals object, can be same (ptr)
	;outputs
	;:r0 = reals object (ptr)
	;:r1 = dot product (real)
	;trashes
	;:r1-:r4, :f0-:f1

	(vp-rdef (this src2 src1 idx1 idx2))
	(vp-fdef (dot e0 e1))

	(entry :reals :dot `(,this ,src2))

	(vec-reduce-idx (list this src2) (list e0 e1) (list src1 src2) (list idx1 idx2)
		(# (vp-mul-ff %0 %1) (vp-add-ff %1 dot))
		(# (vp-sub-ff dot dot)))

	(exit :reals :dot `(,this ,dot))
	(vp-ret)

(def-func-end)

(def-method :reals :quant)
	;inputs
	;:r0 = reals object (dst)
	;:r1 = source reals object (src)
	;:r2 = tolerance real (real)
	;outputs
	;:r0 = reals object (ptr)
	;trashes
	;:r1-:r5, :f0-:f4

	(vp-rdef (dst src1 dst1 src_end trunc))
	(vp-fdef (e0 tol half one ftrunc))

	(entry :reals :quant `(,dst ,src1 ,tol))

	; Load constants
	(vp-lea-p 'fn_consts trunc)
	(load-fields trunc
		(fn-consts (n2r 0.5) (n2r 1.0))
		`(,half ,one))

	(assign `((,src1 +array_length)) `(,src_end))
	(vp-simd vp-cpy-ir `(,src1 ,dst) `(,+array_begin) `(,src1 ,dst1))
	(vp-shl-cr (log2 +ptr_size) src_end)
	(vp-add-rr src1 src_end)

	(vec-loop-until `(,src1) `(,e0) src_end dst1
		(#  (vp-div-ff tol %0)
			(vp-cvt-fr %0 trunc)
			(vp-cvt-rf trunc ftrunc)
			(vpif `(,ftrunc > ,%%0))
				(vp-sub-ff one ftrunc)
			(endif)
			(vp-add-ff half ftrunc)
			(vp-mul-ff tol ftrunc)
			ftrunc))

	(exit :reals :quant `(,dst))
	(vp-ret)

(def-func-end)

(def-method :reals :mat4x4_mul)
	;inputs
	;:r0 = output reals object (ptr)
	;:r1 = ma reals object (ptr)
	;:r2 = mb reals object (ptr)
	;outputs
	;:r0 = output reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r10, :f0-:f15

	(vp-rdef (out ma mb data_o data_a data_b)
		'(:r0 :r1 :r2 :r3 :r4 :r5))
	(vp-fdef (a0 a1 a2 a3 b0 b1 b2 b3)
		'(:f0 :f1 :f2 :f3 :f4 :f5 :f6 :f7))

	(entry :reals :mat4x4_mul `(,out ,ma ,mb))

	(assign `((,ma +array_begin) (,mb +array_begin) (,out +array_begin))
		`(,data_a ,data_b ,data_o))

	(defq row 0)
	(times 4
		; Load Row A (4 elements, 8 bytes each = 32 bytes stride)
		(vp-simd vp-cpy-if `(,data_a)
			`(,(* row 32) ,(+ (* row 32) 8) ,(+ (* row 32) 16) ,(+ (* row 32) 24))
			`(,a0 ,a1 ,a2 ,a3))

		(defq col 0)
		(times 4
			; Load column of B (strided for column-major storage)
			(vp-simd vp-cpy-if `(,data_b)
				`(,(* col 8) ,(+ (* col 8) 32) ,(+ (* col 8) 64) ,(+ (* col 8) 96))
				`(,b0 ,b1 ,b2 ,b3))
			(vp-simd vp-mul-ff `(,a0 ,a1 ,a2 ,a3) `(,b0 ,b1 ,b2 ,b3))
			(vp-simd vp-add-ff `(,b1 ,b3) `(,b0 ,b2))
			(vp-add-ff b2 b0)

			(vp-cpy-fi b0 data_o (+ (* row 32) (* col 8)))
			(setq col (inc col)))
		(setq row (inc row)))

	(exit :reals :mat4x4_mul `(,out -1))
	(vp-ret)

(def-func-end)

(def-method :reals :mat4x4_v4_mul)
	;inputs
	;:r0 = output reals object (ptr)
	;:r1 = matrix ma reals object (ptr)
	;:r2 = vector v reals object (ptr)
	;outputs
	;:r0 = output reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r10, :f0-:f7

	(vp-rdef (out ma v data_o data_a data_v)
		'(:r0 :r1 :r2 :r3 :r4 :r5))
	(vp-fdef (a0 a1 a2 a3 v0 v1 v2 v3)
		'(:f0 :f1 :f2 :f3 :f4 :f5 :f6 :f7))

	(entry :reals :mat4x4_v4_mul `(,out ,ma ,v))

	(assign `((,ma +array_begin) (,v +array_begin) (,out +array_begin))
		`(,data_a ,data_v ,data_o))

	; Load Vector V
	(vp-simd vp-cpy-if `(,data_v) '(0 8 16 24) `(,v0 ,v1 ,v2 ,v3))

	(defq row 0)
	(times 4
		; Load Row A
		(vp-simd vp-cpy-if `(,data_a)
			`(,(* row 32) ,(+ (* row 32) 8) ,(+ (* row 32) 16) ,(+ (* row 32) 24))
			`(,a0 ,a1 ,a2 ,a3))

		(vp-simd vp-mul-ff `(,v0 ,v1 ,v2 ,v3) `(,a0 ,a1 ,a2 ,a3))
		(vp-simd vp-add-ff `(,a1 ,a3) `(,a0 ,a2))
		(vp-add-ff a2 a0)

		; Write Component
		(vp-cpy-fi a0 data_o (* row 8))
		(setq row (inc row)))

	(exit :reals :mat4x4_v4_mul `(,out -1))
	(vp-ret)

(def-func-end)

(def-method :reals :mat3x3_mul)
	;inputs
	;:r0 = output reals object (ptr)
	;:r1 = ma reals object (ptr)
	;:r2 = mb reals object (ptr)
	;outputs
	;:r0 = output reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r10, :f0-:f15

	(vp-rdef (out ma mb data_o data_a data_b)
		'(:r0 :r1 :r2 :r3 :r4 :r5))
	(vp-fdef (a0 a1 a2 b0 b1 b2)
		'(:f0 :f1 :f2 :f3 :f4 :f5))

	(entry :reals :mat3x3_mul `(,out ,ma ,mb))

	(assign `((,ma +array_begin) (,mb +array_begin) (,out +array_begin))
		`(,data_a ,data_b ,data_o))

	(defq r 0)
	(times 3
		; Load Row A (3 elements, 8 bytes each = 24 bytes stride)
		(vp-simd vp-cpy-if `(,data_a)
			`(,(* r 24) ,(+ (* r 24) 8) ,(+ (* r 24) 16))
			`(,a0 ,a1 ,a2))

		(defq c 0)
		(times 3
			; Load column of B (strided for column-major storage)
			(vp-simd vp-cpy-if `(,data_b)
				`(,(* c 8) ,(+ (* c 8) 24) ,(+ (* c 8) 48))
				`(,b0 ,b1 ,b2))
			(vp-simd vp-mul-ff `(,a0 ,a1 ,a2) `(,b0 ,b1 ,b2))
			(vp-add-ff b1 b0)
			(vp-add-ff b2 b0)

			(vp-cpy-fi b0 data_o (+ (* r 24) (* c 8)))
			(setq c (inc c)))
		(setq r (inc r)))

	(exit :reals :mat3x3_mul `(,out -1))
	(vp-ret)

(def-func-end)

(def-method :reals :mat3x3_v3_mul)
	;inputs
	;:r0 = output reals object (ptr)
	;:r1 = matrix ma reals object (ptr)
	;:r2 = vector v reals object (ptr)
	;outputs
	;:r0 = output reals object (ptr)
	;:r1 = 0 if error, else ok
	;trashes
	;:r1-:r10, :f0-:f7

	(vp-rdef (out ma v data_o data_a data_v)
		'(:r0 :r1 :r2 :r3 :r4 :r5))
	(vp-fdef (a0 a1 a2 v0 v1 v2)
		'(:f0 :f1 :f2 :f3 :f4 :f5))

	(entry :reals :mat3x3_v3_mul `(,out ,ma ,v))

	(assign `((,ma +array_begin) (,v +array_begin) (,out +array_begin))
		`(,data_a ,data_v ,data_o))

	; Load Vector V
	(vp-simd vp-cpy-if `(,data_v) '(0 8 16) `(,v0 ,v1 ,v2))

	(defq r 0)
	(times 3
		; Load Row A
		(vp-simd vp-cpy-if `(,data_a)
			`(,(* r 24) ,(+ (* r 24) 8) ,(+ (* r 24) 16))
			`(,a0 ,a1 ,a2))

		(vp-simd vp-mul-ff `(,v0 ,v1 ,v2) `(,a0 ,a1 ,a2))
		(vp-add-ff a1 a0)
		(vp-add-ff a2 a0)

		; Write Component
		(vp-cpy-fi a0 data_o (* r 8))
		(setq r (inc r)))

	(exit :reals :mat3x3_v3_mul `(,out -1))
	(vp-ret)

(def-func-end)
