(include "lib/asm/func.inc")
(include "sys/math/class.inc")
(include "./class.inc")
(include "././sym/class.inc")
(include "././stream/class.inc")
(include "././list/class.inc")

(gen-create :real)
(gen-vtable :real)
(gen-type :real)

(def-method :real :print)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = stream object (ptr)
	;outputs
	;:r0 = real object (ptr)
	;trashes
	;:r1-:r14

	(def-vars
		(ptr this stream tmp))

	(entry :real :print '(:r0 :r1))

	(push-scope)
	(assign '(:r0 :r1) {this, stream})
	(call :stream :write_cstr '(:r1 "0x"))
	(call :str :create_from_long {this->num_value, -16} {tmp})
	(call :stream :write {stream, &tmp->str_data, tmp->str_length})
	(call :obj :deref {tmp})

	(exit :real :print {this})
	(pop-scope)
	(return)

(def-func-end)

(redefun addop (%0)
	(entry :real %0 '(:r0 :r1))
	(class/array/get_both :r1 :r1 :r2)
	(vp-cpy-ir :r1 0 :r0)
	(vp-add-cr +ptr_size :r1)
	(assign '((:r0 num_value)) '(:f0))
	(loop-start)
		(vp-cpy-ir :r1 0 :r4)
		(vp-add-cr +ptr_size :r1)
		(assign '((:r4 num_value)) '(:f1))
		((eval (sym (cat "vp-" (rest %0) "-ff"))) :f1 :f0)
	(loop-until '(:r1 = :r2))
	(v-jump :num :vcreate '(:f0) :r4 :r1))

(def-method :real :add)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r1-:r14, :f0-:f1

	(addop :add)

(def-func-end)

(def-method :real :sub)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r1-:r14, :f0-:f1

	(addop :sub)

(def-func-end)

(def-method :real :mul)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r0 = real object (ptr)
	;:r1 = result real object (ptr)
	;trashes
	;:r1-:r14, :f0-:f1

	(addop :mul)

(def-func-end)

(redefun minop (%0 p)
	(vp-rdef (this args obj iter_begin iter_end))
	(vp-fdef (val new_val))
	(entry :real %0 `(,this ,args))
	(class/array/get_both args iter_begin iter_end)
	(vp-cpy-ir iter_begin 0 args)
	(vp-add-cr +ptr_size iter_begin)
	(assign `((,args num_value)) `(,val))
	(loop-start)
		(vp-cpy-ir iter_begin 0 obj)
		(vp-add-cr +ptr_size iter_begin)
		(assign `((,obj num_value)) `(,new_val))
		(exec p)
	(loop-until `(,iter_begin = ,iter_end))
	(class/obj/ref args iter_begin)
	(exit :real %0 `(,args))
	(vp-ret))

(def-method :real :min)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r1 = result real object (ptr)
	;trashes
	;:r1-:r14, :f0-:f1

	(minop :min
		'(progn
			(vpif `(,new_val < ,val))
				(vp-cpy-ff new_val val)
				(vp-cpy-rr obj args)
			(endif)))

(def-func-end)

(def-method :real :max)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r1 = result real object (ptr)
	;trashes
	;:r1-:r14, :f0-:f1

	(minop :max
		'(progn
			(vpif `(,new_val > ,val))
				(vp-cpy-ff new_val val)
				(vp-cpy-rr obj args)
			(endif)))

(def-func-end)

(redefun divop (%0 p)
	(entry :real %0 '(:r0 :r1))
	(class/array/get_both :r1 :r1 :r4)
	(vp-cpy-ir :r1 0 :r0)
	(vp-add-cr +ptr_size :r1)
	(assign '((:r0 num_value) 0) '(:f0 :r3))
	(vp-cpy-rf :r3 :f3)
	(loop-start)
		(vp-cpy-ir :r1 0 :r5)
		(vp-add-cr +ptr_size :r1)
		(assign '((:r5 num_value)) '(:f1))
	(errorcase
		(gotoif '(:f1 = :f3) 'error))
		(exec p)
	(loop-until '(:r1 = :r4))
	(v-jump :num :vcreate '(:f0) :r5 :r1)
(vp-label 'error)
	(exit :real %0 '(0))
	(vp-ret))

(def-method :real :div)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r0 = 0 if error, else result real object (ptr)
	;trashes
	;:r1-:r14, :f0-:f3

	(divop :div
		'(vp-div-ff :f1 :f0))

(def-func-end)

(def-method :real :mod)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r0 = 0 if error, else result real object (ptr)
	;trashes
	;:r1-:r14, :f0-:f3

	(divop :mod
		'(progn
			(vp-cpy-ff :f0 :f2)		; Copy dividend (x)
			(vp-div-ff :f1 :f2)		; f2 = x / y
			(vp-cvt-fr :f2 :r0)		; r0 = (int)(x / y) [Truncate]
			(vp-cvt-rf :r0 :f2)		; f2 = (double)(int)(x / y)
			(vp-mul-ff :f1 :f2)		; f2 = y * trunc(x / y)
			(vp-sub-ff :f2 :f0)))	; f0 = x - (y * trunc(x / y))

(def-func-end)

(redefun compareop (%0 p)
	(entry :real %0 '(:r0 :r1))
	(class/array/get_both :r1 :r1 :r2)
	(vp-cpy-ir :r1 0 :r3)
	(vp-add-cr +ptr_size :r1)
	(assign '((:r3 num_value)) '(:f0))
	(loop-start)
		(vp-cpy-ir :r1 0 :r3)
		(vp-add-cr +ptr_size :r1)
		(assign '((:r3 num_value)) '(:f1))
		(exec p)
	(loop-until '(:r1 = :r2))
	(exit :real %0 '(-1))
	(vp-ret)
(vp-label 'fail)
	(exit :real %0 '(0))
	(vp-ret))

(def-method :real :ge)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r1 = 0, -1 (int)
	;trashes
	;:r1-:r14, :f0-:f1

	(compareop :ge
		'(progn
			(gotoif '(:f0 < :f1) 'fail)
			(vp-cpy-ff :f1 :f0)))

(def-func-end)

(def-method :real :gt)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r1 = 0, -1 (int)
	;trashes
	;:r1-:r14, :f0-:f1

	(compareop :gt
		'(progn
			(gotoif '(:f0 <= :f1) 'fail)
			(vp-cpy-ff :f1 :f0)))

(def-func-end)

(def-method :real :le)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r1 = 0, -1 (int)
	;trashes
	;:r1-:r14, :f0-:f1

	(compareop :le
		'(progn
			(gotoif '(:f0 > :f1) 'fail)
			(vp-cpy-ff :f1 :f0)))

(def-func-end)

(def-method :real :lt)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = list of real objects (ptr)
	;outputs
	;:r1 = 0, -1 (int)
	;trashes
	;:r1-:r14, :f0-:f1

	(compareop :lt
		'(progn
			(gotoif '(:f0 >= :f1) 'fail)
			(vp-cpy-ff :f1 :f0)))

(def-func-end)

;generic real math ops
(redefun mathop (%0 p)
	(entry :real %0 '(:r0))
	(assign '((:r0 num_value)) '(:f1))
	(exec p)
	(jump :real :create '(:f0)))

(def-method :real :sqrt)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f1

	(mathop :sqrt
		'(vp-sqrt-ff :f1 :f0))

(def-func-end)

(def-method :real :sin)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f3

	(mathop :sin
		'(call :sys_math :r_sin '(:f1) '(:f0)))

(def-func-end)

(def-method :real :cos)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f3

	(mathop :cos
		'(progn
			;pi/2
			(vp-lea-p 'fn_consts :r0)
			(assign '((:r0 (fn-const 0x3ff921fb54442d18))) '(:f0))
			(vp-add-ff :f0 :f1)
			(call :sys_math :r_sin '(:f1) '(:f0))))

(def-func-end)

(def-method :real :frac)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f1

	(mathop :frac
		'(progn
			(vp-lea-p 'fn_consts :r1)
			(vp-cpy-ff :f1 :f0)
			(vp-cvt-fr :f0 :r0)
			(vp-cvt-rf :r0 :f1)
			(vp-sub-ff :f1 :f0)
			(assign '((:r1 (fn-const (n2r 0)))) '(:f1))
			(vpif '(:f0 < :f1))
				(assign '((:r1 (fn-const (n2r 1)))) '(:f1))
				(vp-add-ff :f1 :f0)
			(endif)))

(def-func-end)

(def-method :real :floor)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f1

	(mathop :floor
		'(progn
			(vp-cvt-fr :f1 :r0)
			(vp-cvt-rf :r0 :f0)
			(vpif '(:f0 > :f1))
				(vp-lea-p 'fn_consts :r1)
				(assign '((:r1 (fn-const (n2r 1)))) '(:f1))
				(vp-sub-ff :f1 :f0)
			(endif)))

(def-func-end)

(def-method :real :ceil)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f1

	(mathop :ceil
		'(progn
			(vp-cvt-fr :f1 :r0)
			(vp-cvt-rf :r0 :f0)
			(vpif '(:f0 < :f1))
				(vp-lea-p 'fn_consts :r1)
				(assign '((:r1 (fn-const (n2r 1)))) '(:f1))
				(vp-add-ff :f1 :f0)
			(endif)))

(def-func-end)

(def-method :real :recip)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f1

	(mathop :recip
		'(progn
			(vp-lea-p 'fn_consts :r1)
			(assign '((:r1 (fn-const (n2r 1)))) '(:f0))
			(vp-div-ff :f1 :f0)))

(def-func-end)

(def-method :real :abs)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f1

	(mathop :abs
		'(vp-abs-ff :f1 :f0))

(def-func-end)

(def-method :real :neg)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f1

	(mathop :neg
		'(vp-neg-ff :f1 :f0))

(def-func-end)

(def-method :real :sign)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f1

	(mathop :sign
		'(progn
			(vp-lea-p 'fn_consts :r1)
			(assign '((:r1 (fn-const (n2r 0)))) '(:f0))
			(vpif '(:f1 > :f0))
				(assign '((:r1 (fn-const (n2r 1)))) '(:f0))
			(elseif '(:f1 < :f0))
				(assign '((:r1 (fn-const (n2r -1)))) '(:f0))
			(endif)))

(def-func-end)

(def-method :real :random)
	;inputs
	;:r0 = real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f1

	(mathop :random
		'(progn
			(vp-lea-p 'fn_consts :r4)
			(assign '((:r4 (fn-const (n2r 1)))) '(:f0))
			(vp-mul-ff :f0 :f1)
			(vp-cvt-fr :f1 :r0)
			(call :sys_math :i_rand '(:r0) '(:r0))
			(vp-cvt-rf :r0 :f0)
			(assign '((:r4 (fn-const (recip (n2r 1))))) '(:f1))
			(vp-mul-ff :f1 :f0)))

(def-func-end)

(def-method :real :quant)
	;inputs
	;:r0 = real object (ptr)
	;:r1 = tolerance real object (ptr)
	;outputs
	;:r0 = result real object (ptr)
	;trashes
	;:r0-:r14, :f0-:f3

	(vp-rdef (this tol trunc))
	(vp-fdef (v tmp half one ftrunc))

	(entry :real :quant `(,this ,tol))

	(assign `((,this num_value) (,tol num_value)) `(,v ,tmp))

	; val / tol
	(vp-div-ff tmp v)
	(vp-lea-p 'fn_consts tol)

	; floor(v)
	(vp-cvt-fr v trunc)
	(vp-cvt-rf trunc ftrunc)
	(vpif `(,ftrunc > ,v))
		(assign `((,tol (fn-const (n2r 1.0)))) `(,one))
		(vp-sub-ff one ftrunc)
	(endif)
	(vp-cpy-ff ftrunc v)

	; + 0.5
	(assign `((,tol (fn-const (n2r 0.5)))) `(,half))
	(vp-add-ff half v)

	; * tol
	(vp-mul-ff tmp v)
	(jump :real :create `(,v))

(def-func-end)
