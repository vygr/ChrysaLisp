(import 'sys/func.inc)
(import 'class/function/class.inc)
(import 'class/lisp/class.inc)

(def-method 'lisp 'repl_apply)
	;inputs
	;r0 = lisp object (ptr)
	;r1 = args vector object (ptr)
	;r2 = function object (ptr)
	;outputs
	;r0 = lisp object (ptr)
	;r1 = return value object (ptr)
	;trashes
	;all but r0

	(entry 'lisp 'repl_apply '(r0 r1 r2))

	(assign `((r2 obj_vtable) (@ ,(f-path 'function 'vtable))) '(r3 r4))
	(vpif '(r3 == r4))
		;built in or compiled function
		(vp-cpy-ir r2 function_value r2)
		(vp-and-cr (neg ptr_size) r2)
		(vp-jmp-r r2)
	(endif)

	(ptr 'this 'args)
	(union
		'(ptr 'func)
		'(pptr 'iter_end))
	(ptr 'value)
	(pptr 'iter_begin)
(errorcases
	(uint 'length))

	(push-scope)
	(assign '(r0 r1 r2) {this, args, func})

	;lambda ?
(errorcases
	(errorif (cat {func->obj_vtable != @} (f-path 'vector 'vtable)) 'error2)
	(d-call 'vector 'get_length {func} {_, length})
	(errorif {length < 2} 'error1))

	(call 'vector 'get_iters {func} {_, iter_begin, iter_end})

(errorcases
	(assign {iter_begin[ptr_size * 0]} {value})
	(vpif {value != this->lisp_sym_lambda})
		(errorif {value != this->lisp_sym_macro} 'error2)
	(endif))

	(call 'lisp 'env_push {this})
	(call 'lisp 'env_bind {this, iter_begin[ptr_size * 1], args} {_, value})
	(errorif (cat {value->obj_vtable == @} (f-path 'error 'vtable)) 'exit1)

	(assign {&iter_begin[ptr_size * 2]} {iter_begin})
	(loop-while {iter_begin != iter_end})
		(call 'obj 'deref {value})
		(call 'lisp 'repl_eval {this, *iter_begin} {_, value})
		(errorif (cat {value->obj_vtable == @} (f-path 'error 'vtable)) 'exit1)
		(assign {iter_begin + ptr_size} {iter_begin})
	(loop-end)

(vp-label 'exit1)
	(call 'lisp 'env_pop {this})
(vp-label 'funcexit)
	(exit 'lisp 'repl_apply {this, value})
	(return)

(errorcases
(vp-label 'error1)
	(call 'lisp 'repl_error {this, "(lambda ([arg ...]) body)", error_msg_wrong_num_of_args, func} {_, value})
	(goto 'funcexit)
(vp-label 'error2)
	(call 'lisp 'repl_error {this, "(lambda ([arg ...]) body)", error_msg_not_a_lambda, func} {_, value})
	(goto 'funcexit))

	(pop-scope-syms)

(def-func-end)
