(include 'sys/func.inc)
(include 'class/nums/class.inc)
(include 'sys/math/class.inc)

(gen-create 'nums)
(gen-vtable 'nums)

(defun-bind addop (_)
	;trashes
	;r1-r6+
	(vp-def (dst src1 src2 iter_begin iter_end))
	(entry 'nums _ `(,dst ,src1 ,src2))
	(class/array/get_both dst iter_begin iter_end)
	(assign `((,src1 array_begin) (,src2 array_begin)) `(,src1 ,src2))
	(vec-set 5)
	(loop-start)
		(vec-load-long 1 src1)
		(vec-load-long 1 src2)
		((eval (sym (cat "vec-" _))) 1)
		(vec-store-long 1 iter_begin)
		(vp-add-cr long_size iter_begin)
		(vp-add-cr long_size src1)
		(vp-add-cr long_size src2)
	(loop-until (list iter_begin '= iter_end))
	(exit 'nums _ `(,dst))
	(vp-ret))

(defun-bind divop (_)
	;trashes
	;r1-r8+
	(vp-def (s1 s2 dst src1 src2 iter_begin iter_end) '(r1 r2 r4 r5 r6 r7 r8))
	(entry 'nums _ `(,dst ,s1 ,s2))
	(class/array/get_both dst iter_begin iter_end)
	(assign `((,s1 array_begin) (,s2 array_begin)) `(,src1 ,src2))
	(vec-set 0)
	(loop-start)
		(vec-load-long 1 src1)
		(vec-load-long 1 src2)
		((eval (sym (cat "vec-" _))) 1)
		(vec-store-long 1 iter_begin)
		(vp-add-cr long_size iter_begin)
		(vp-add-cr long_size src1)
		(vp-add-cr long_size src2)
	(loop-until (list iter_begin '= iter_end))
	(exit 'nums _ `(,dst))
	(vp-ret))

(defun-bind absop (_)
	;trashes
	;r1-r4+
	(vp-def (dst src iter_begin iter_end))
	(entry 'nums _ `(,dst ,src))
	(class/array/get_both dst iter_begin iter_end)
	(assign `((,src array_begin)) `(,src))
	(vec-set 4)
	(loop-start)
		(vec-load-long 1 src)
		((eval (sym (cat "vec-" _))) 1)
		(vec-store-long 1 iter_begin)
		(vp-add-cr long_size iter_begin)
		(vp-add-cr long_size src)
	(loop-until (list iter_begin '= iter_end))
	(exit 'nums _ `(,dst))
	(vp-ret))

(def-method 'nums 'add)
	;inputs
	;r0 = nums object (ptr)
	;r1 = source1 nums object, can be same (ptr)
	;r2 = source2 nums object, can be same (ptr)
	;outputs
	;r0 = nums object (ptr)
	;trashes
	;r1-r6

	(addop 'add)

(def-func-end)

(def-method 'nums 'sub)
	;inputs
	;r0 = nums object (ptr)
	;r1 = source1 nums object, can be same (ptr)
	;r2 = source2 nums object, can be same (ptr)
	;outputs
	;r0 = nums object (ptr)
	;trashes
	;r1-r6

	(addop 'sub)

(def-func-end)

(def-method 'nums 'mul)
	;inputs
	;r0 = nums object (ptr)
	;r1 = source1 nums object, can be same (ptr)
	;r2 = source2 nums object, can be same (ptr)
	;outputs
	;r0 = nums object (ptr)
	;trashes
	;r1-r6

	(addop 'mul)

(def-func-end)

(def-method 'nums 'div)
	;inputs
	;r0 = nums object (ptr)
	;r1 = source1 nums object, can be same (ptr)
	;r2 = source2 nums object, can be same (ptr)
	;outputs
	;r0 = nums object (ptr)
	;trashes
	;r1-r8

	(divop 'div)

(def-func-end)

(def-method 'nums 'mod)
	;inputs
	;r0 = nums object (ptr)
	;r1 = source1 nums object, can be same (ptr)
	;r2 = source2 nums object, can be same (ptr)
	;outputs
	;r0 = nums object (ptr)
	;trashes
	;r1-r8

	(divop 'mod)

(def-func-end)

(def-method 'nums 'abs)
	;inputs
	;r0 = nums object (ptr)
	;r1 = source nums object, can be same (ptr)
	;outputs
	;r0 = nums object (ptr)
	;trashes
	;r1-r4

	(absop 'abs)

(def-func-end)

(def-method 'nums 'sum)
	;inputs
	;r0 = nums object (ptr)
	;outputs
	;r0 = nums object (ptr)
	;r1 = sum (long)
	;trashes
	;r1-r4

	(vp-def (dst iter_begin iter_end sum val))

	(entry 'nums 'sum `(,dst))

	(call 'nums 'get_both `(,dst) `(,dst ,iter_begin ,iter_end))
	(vp-xor-rr sum sum)
	(loop-while `(,iter_begin /= ,iter_end))
		(assign `((,iter_begin 0)) `(,val))
		(vp-add-cr long_size iter_begin)
		(vp-add-rr val sum)
	(loop-end)

	(exit 'nums 'sum `(,dst ,sum))
	(vp-ret)

(def-func-end)

(def-method 'nums 'scale)
	;inputs
	;r0 = nums object (ptr)
	;r1 = source nums object, can be same (ptr)
	;r2 = scale (int)
	;outputs
	;r0 = nums object (ptr)
	;trashes
	;r1-r5

	(vp-def (dst src scale iter_begin iter_end))

	(entry 'nums 'scale `(,dst ,src ,scale))

	(class/array/get_both dst iter_begin iter_end)
	(assign `((,src array_begin)) `(,src))
	(vec-set 5)
	(loop-start)
		(vec-load-long 1 src)
		(vec-mul 1 `(,scale))
		(vec-store-long 1 iter_begin)
		(vp-add-cr long_size iter_begin)
		(vp-add-cr long_size src)
	(loop-until `(,iter_begin = ,iter_end))

	(exit 'nums 'scale `(,dst))
	(vp-ret)

(def-func-end)
